CATALOG JSR CLOSE7
	LDX #$70
	LDA # <DIRNAME
	STA ICBADR,X
	LDA # >DIRNAME
	STA ICBADR+1,X
	LDA #5
	STA ICBLEN,X
	LDA #0
	STA ICBLEN+1,X
	LDA #6
	STA ICAUX1,X
	LDA #3
	STA ICCOM,X
	JSR CIO
	BMI CLOSE7
	LDA #0
	STA XPTR
REDIR LDX XPTR
	LDA $64
	STA SLOT,X
	LDA $65
	STA SLOT+1,X
	INC XPTR
	INC XPTR
	JSR GET7
	BMI CLOSE7
	CMP #'*+1
	BCS ENDIR
	JSR CHROUT
	JSR GET7
	BMI CLOSE7
	LDA #0
	STA DIRCOUNT
DIRLOOP JSR GET7
	BMI CLOSE7
DNOTCR JSR CHROUT
	INC DIRCOUNT
	LDA DIRCOUNT
	CMP #8
	BNE DNOT8
	LDA #'.
	JSR CHROUT
	JMP DIRLOOP
DNOT8 CMP #11
	BNE DIRLOOP
	LDA #5
	STA TEMP
THROW5 JSR GET7
	DEC TEMP
	LDA TEMP
	BNE THROW5
	JMP REDIR
CLOSE7 LDX #$70
	LDA #12
	STA ICCOM,X
	JSR CIO
	LDX #$70
	LDY ICSTAT,X
	RTS
ENDIR PHA
	LDA #155
	JSR CHROUT
	PLA
	JSR CHROUT
ENDLP JSR GET7
	BMI CLOSE7
	JSR CHROUT
	JMP ENDLP
GET7 LDX #$70
	LDA #0
	STA ICBLEN,X
	STA ICBLEN+1,X
	LDA #7
	STA ICCOM,X
	JMP CIO
DOS JSR DELITE
	JSR OPENEDITOR
	JSR DELITE
	LDA #1
	STA 752
	STA 82
	LDA #125
	JSR CHROUT
	JSR CATALOG
	JSR DOSMSG
GETNAME LDA SLOT
	STA SCR
	LDA SLOT+1
	STA SCR+1
	LDA #0
	STA XSLOT
	DEC XPTR
	DEC XPTR
NAMELP JSR INVNAME
	JSR GETAKEY
	LDX #1
	STX 752
	LDX DOSTABLE
	CMP #97
	BCC NOPROB
	AND #95
NOPROB STA TEMP
FINDIT CMP DOSTABLE,X
	BEQ FOUNDIT
	DEX
	BNE FINDIT
	JMP JNAME
FOUNDIT DEX
	TXA
	ASL A
	TAX
	LDA DOSADR+1,X
	PHA
	LDA DOSADR,X
	PHA
	RTS
DOSTABLE .BYTE 15
	.BYTE "RLUF1234"
DOSADR .WORD DLEFT-1,DRIGHT-1,DUP-1,DDOWN-1,DELFILE-1,RENAME-1
	.WORD LOCK-1,UNLOCK-1,FORMAT-1,DRIVE-1,DRIVE-1,DRIVE-1
	.WORD DRIVE-1,ESCDOS-1,LOADIT-1
DLEFT JSR INVNAME
	LDX XSLOT
	BEQ NRANGE
	DEX
	DEX
	JMP RESLOT
DRIGHT JSR INVNAME
	LDX XSLOT
	INX
	INX
	CPX XPTR
	BCS NRANGE
RESLOT STX XSLOT
	LDA SLOT,X
	STA SCR
	LDA SLOT+1,X
	STA SCR+1
NRANGE JMP NAMELP
DUP JSR INVNAME
	LDA XSLOT
	CMP #6
	BCC NRANGE
	SEC
	SBC #6
	TAX
	JMP RESLOT
DDOWN JSR INVNAME
	LDA XSLOT
	CLC
	ADC #6
	CMP XPTR
	BCS NRANGE
	TAX
	JMP RESLOT
NAMER LDX #0
COPYD LDA DIRNAME,X
	STA FNBUFF,X
	INX
	CPX #3
	BNE COPYD
	LDY #1
COPYNAME LDA (SCR),Y
	AND #127
	JSR INTOAS
	CMP #32
	BEQ NOSTOR
	STA FNBUFF,X
	INX
NOSTOR INY
	CPY #13
	BNE COPYNAME
	LDA FNBUFF-1,X
	CMP #'.
	BNE NOTDOT
	DEX
NOTDOT STX FNLEN
	LDA #0
	STA FNBUFF,X
	RTS
XIO LDX #$70
	STA ICCOM,X
	LDA FNLEN
	STA ICBLEN,X
	LDA #0
	STA ICBLEN+1,X
	LDA # <FNBUFF
	STA ICBADR,X
	LDA # >FNBUFF
	STA ICBADR+1,X
	JMP CIO
DELFILE JSR NAMER
	LDA #33
GOXIO JSR XIO
	BPL JNAME
	JMP DOSERR
JNAME JSR INVNAME
	JMP NAMELP
LOCK JSR NAMER
	LDA #35
	JMP GOXIO
UNLOCK JSR NAMER
	LDA #36
	JMP GOXIO
RENAME JSR BOTCLR
	LDA # <RENMSG
	LDY # >RENMSG
	JSR PRMSG
	LDA #64
	STA $02BE
	JSR INPUT
	LDA #0
	STA $02BE
	LDA INLEN
	BEQ NONAME
	JSR NAMER
	LDX #0
	LDY FNLEN
	LDA #',
	STA FNBUFF,Y
	INY
COPYR LDA INBUFF,X
	STA FNBUFF,Y
	INY
	INX
	CPX INLEN
	BNE COPYR
	STY FNLEN
	LDA #0
	STA FNBUFF,Y
	JSR DOSMSG
	LDA #32
	JMP GOXIO
NONAME JSR DOSMSG
	JMP JNAME
FORMAT JSR BOTCLR
	LDA # <FORMSG
	LDY # >FORMSG
	JSR PRMSG
	JSR YORN
	BNE NONAME
	JSR DOSMSG
	JSR NAMER
	LDA #254
	JMP GOXIO
DRIVE LDA TEMP
	STA DIRNAME+1
	JMP DOS
LOADIT LDX #$70
	STX IOCB
	LDA #4
	STA ICAUX1,X
	LDA #0
	STA INDIR
	STA INDIR+1
	JSR NAMER
	LDA #3
	JSR XIO
	BMI DOSERR
	JSR ERASE
	JSR LOADLINK
	BMI DOSERR
ESCDOS LDX #$FA
	TXS
	LDA #125
	JSR CHROUT
	JSR HIGHLIGHT
	JSR SYSMSG
	JMP MAIN
BOTCLR LDA #22
	STA 84
	LDA #157
	JSR CHROUT
	JMP CHROUT
DOSERR STY YSAVE
	JSR CLOSE7
	JSR BOTCLR
	LDA # <ERRMSG
	LDY # >ERRMSG
	JSR PRMSG
	LDX YSAVE
	LDA #0
	JSR OUTNUM
	LDA #253
	JSR CHROUT
	LDA # <DIRMSG
	LDY # >DIRMSG
	JSR PRMSG
	JSR GETAKEY
	JSR DOSMSG
	JMP JNAME
INVNAME LDY #12
INVLP LDA (SCR),Y
	EOR #128
	STA (SCR),Y
	DEY
	BPL INVLP
	RTS
DOSMSG JSR BOTCLR
	LDA # <DIRINS
	LDY # >DIRINS
	JSR PRMSG
	LDA DIRNAME+1
	JMP CHROUT

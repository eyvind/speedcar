;*******************
;*                 *
;* SPEEDSCRIPT:    *
;*                 *
;*       D:SPEED.0 *
;*                 *
;*******************

; Locations used by high-speed memory move
; routines.
FROML = $80
FROMH = $81
DESTL = $82
DESTH = $83
LLEN  = $84
HLEN  = $85

; CURR: Position of cursor within text memory. SCR:
; used by the REFRESH routine.
CURR = $86
SCR  = $88

; TEX: An alternate location used in tandem with
; CURR. COLR is used by REFRESH. TEMP is used
; throughout as a scratchpad pointer. INDIR is also
; a reusable indirect pointer. UNDERCURS stores the
; value of the character highlighted by the cursor.
TEX       = $8A
TEMP      = $8C
INDIR     = $8E
UNDERCURS = $90

; WINDCOLOR: Color of command line window supported
; by HIGHLIGHT. RETCHAR is the screen-code value of
; the return-mark (a left-pointing arrow). SPACE is
; the screen-code value of the space character, RED
; and BLUE are used as command-line colors
WINDCOLR = $91
RETCHAR  = 94
SPACE    = 0
RED      = $32
BLUE     = $74

; Input/Output Control System definitions for
; input/output control blocks (IOCBs). CIO is the
; entry point for all file-oriented input/output.
; SHFLOK is the SHiFtLOcK flag.
ICCOM  = $0342
ICBADR = $0344
ICBLEN = $0348
ICAUX1 = $034A
ICAUX2 = $034B
ICSTAT = $0343
SHFLOK = $02BE
CIO    = $E456

; Allow MEMSTART to be defined on the command line
.IF .NOT .DEF MEMSTART
; Location $1F00 is safely above DOS 2.0S, DOS 3,
; and OS/A+ DOS. Some DOS's may use more memory, so
; you may need to reassemble SpeedScript at a
; higher address, usually the address of LOMEM plus
; 256 bytes to be safe.
MEMSTART = $1F00
.ENDIF

.IF <MEMSTART <> 0
.ERROR "MEMSTART must be page aligned ($xx00)"
.ENDIF

; If NOCOPYCHSET is set when building a cartridge
; image, the character set is not copied to RAM.
; This gives 1024 bytes more text storage but
; means that the "show invisible spaces" function
; (CTRL-O) does not work.
.IF .DEF CARTRIDGE .AND .DEF NOCOPYCHSET
CHSET    = $A000	; Character set
MEMCOPY  = MEMSTART	; Self-modifying code and variables
.ELSE

; CHSET must be aligned to 1024 bytes, but MEMSTART
; is only aligned to 256.  PRBUFF is 256 bytes in
; size and can fill the gap to 512 byte alignment
; if necessary.
CHSET   .= MEMSTART
.IF >CHSET & 1 <> 0
PRBUFF   = CHSET
PRBUFSET = 1		; Declare that PRBUFF is set
CHSET   .= CHSET+$100
.ENDIF

; CHSET is now 512-byte aligned, but might not be
; aligned to 1024 bytes.  HDBUFF and FTBUFF are 256
; bytes each, so they can be used to move CHSET by
; another 512 bytes.
.IF >CHSET & 2 <> 0
HDBUFF   = CHSET
FTBUFF   = CHSET+$100
HFBUFSET = 1		; HDBUFF and FTBUFF are set
CHSET   .= CHSET+$200
.ENDIF

MEMCOPY  = CHSET+$400
.ENDIF

.IF .DEF CARTRIDGE
	*= $A000
.ELSE
	*= CHSET
.ENDIF

CHSET_ .INCLUDE chset.m65

; Called only when run from DOS. It is assumed that
; the author's initials (that conveniently work out
; in hex) are not normally present in memory. If
; they are, we know that SpeedScript has been run
; before, so we avoid the ERASE routine to preserve
; the text in memory.
BEGIN LDA 710
	STA 709
.IF .NOT .DEF CARTRIDGE
	JSR INIT
.ENDIF
	LDA #$CB
	CMP FIRSTRUN
	STA FIRSTRUN
.IF .DEF CARTRIDGE
	BNE DOINIT
	JMP JDOS
DOINIT JSR INIT
.ELSE
	BEQ SKIPERAS
.ENDIF
	JSR ERASE
	JSR KILLBUFF
	LDA #8
	STA SCRCOL
	LDA #2
	STA TEXCOLR
.IF .NOT .DEF CARTRIDGE
	; TODO: handle cassette boot
	LDA $0C
	STA JDOS+1
	LDA $0D
	STA JDOS+2
	LDA # <JDOS
	STA $0C
	LDA # >JDOS
	STA $0D
	LDA #0
	STA 580
	LDA #1
	STA $09
.ELSE
	JSR RAMCOPY
.ENDIF
SKIPERAS JSR INIT2
	JMP MAIN

	.INCLUDE speed1.m65
	.INCLUDE support.m65
	.INCLUDE dospak.m65
	.INCLUDE speed2.m65
	.INCLUDE data.m65

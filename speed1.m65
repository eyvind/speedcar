UMOVE LDA FROML
	STA MOVLOOP+1
	LDA FROMH
	STA MOVLOOP+2
	LDA DESTL
	STA MOVLOOP+4
	LDA DESTH
	STA MOVLOOP+5
	LDX HLEN
	BEQ SKIPMOV
MOV1 LDA #0
MOV2 STA ENDPOS
	LDY #0
MOVLOOP LDA $FFFF,Y
	STA $FFFF,Y
	INY
	CPY ENDPOS
	BNE MOVLOOP
	INC MOVLOOP+2
	INC MOVLOOP+5
	CPX #0
	BEQ OUT
	DEX
	BNE MOV1
SKIPMOV LDA LLEN
	BNE MOV2
OUT RTS
DMOVE LDA HLEN
	TAX
	ORA LLEN
	BNE NOTNULL
	RTS
NOTNULL CLC
	TXA
	ADC FROMH
	STA DMOVLOOP+2
	LDA FROML
	STA DMOVLOOP+1
	CLC
	TXA
	ADC DESTH
	STA DMOVLOOP+5
	LDA DESTL
	STA DMOVLOOP+4
	INX
	LDY LLEN
	BNE DMOVLOOP
	BEQ SKIPDMOV
DMOV1 LDY #255
DMOVLOOP LDA $FFFF,Y
	STA $FFFF,Y
	DEY
	CPY #255
	BNE DMOVLOOP
SKIPDMOV DEC DMOVLOOP+2
	DEC DMOVLOOP+5
	DEX
	BNE DMOV1
	RTS
REFRESH LDA #40
	INY
	CLC
	ADC RLM
	CLC
	ADC $58
	STA SCR
	LDA $59
	ADC #0
	STA SCR+1
	CLC
	LDA TOPLIN
	STA TEX
	LDA TOPLIN+1
	STA TEX+1
	LDX #1
	LDA INSMODE
	STA WINDCOLR
PPAGE LDY #0
PLINE LDA (TEX),Y
	STA LBUFF,Y
	INY
	AND #127
	CMP #RETCHAR
	BEQ BREAK
	CPY LINELEN
	BNE PLINE
	DEY
SLOOP LDA (TEX),Y
	AND #127
NXCUR CMP #SPACE
	BEQ SBRK
	DEY
	BNE SLOOP
	LDY LINELEN
	DEY
SBRK INY
BREAK STY TEMP
	LDY #0
COPY LDA LBUFF,Y
	STA (SCR),Y
	INY
	CPY TEMP
	BNE COPY
	CLC
	TYA
	ADC TEX
	STA TEX
	LDA TEX+1
	ADC #0
	STA TEX+1
	CPX #1
	BNE CLRLN
	STY LENTABLE
CLRLN CPY LINELEN
	BEQ CLEARED
	LDA #64
	STA (SCR),Y
	INY
	JMP CLRLN
CLEARED CLC
	LDA SCR
	ADC #40
	STA SCR
	BCC INCNOT
	INC SCR+1
INCNOT INX
	CPX #19
	BEQ PDONE
	JMP PPAGE
PDONE LDA TEX
	STA BOTSCR
	LDA TEX+1
	STA BOTSCR+1
	RTS
ERASE LDA TEXSTART
	STA TEX
	STA TOPLIN
	STA LASTLINE
	STA CURR
	LDA TEXSTART+1
	STA TEX+1
	STA TOPLIN+1
	STA LASTLINE+1
	STA CURR+1
	SEC
	LDA TEXEND+1
	SBC TEXSTART+1
	TAX
	LDA #SPACE
CLRLOOP LDY #255
	DEC TEX+1
	STA (TEX),Y
	INY
	INC TEX+1
CLR2 STA (TEX),Y
	INY
	BNE CLR2
	INC TEX+1
	DEX
	BNE CLR2
	STA (TEX),Y
	RTS
PRMSG STA TEMP
	STY TEMP+1
	LDA #1
	STA 752
	LDY #0
PRLOOP LDA (TEMP),Y
	BEQ PREXIT
	JSR CHROUT
	INY
	BNE PRLOOP
PREXIT RTS
GETAKEY JSR GETIN
	BEQ GETAKEY
	RTS
JDOS JSR PREXIT
	LDA BLINK
	BEQ NOBLINK
	LDY #0
	LDA UNDERCURS
	STA (CURR),Y
NOBLINK JSR INIT2
	JMP MAIN
INIT LDA #125
	JSR CHROUT
	LDA #0
	STA INSMODE
	STA TEXSTART
	STA TEXEND
	STA TEXBUF
	STA BUFEND
	STA HUNTLEN
	STA REPLEN
	STA ESCFLAG
	STA SHFLOK
	STA RLM
	LDA #40
	STA LINELEN
	LDA # >END
	CLC
	ADC #1
	STA TEXSTART+1
	LDA 561
	SEC
	SBC #1
	STA BUFEND+1
	SEC
	SBC #8
	STA TEXBUF+1
	SEC
	SBC #1
	STA TEXEND+1
	LDA #$FF
	STA FPOS+1
	LDA $4B
	BEQ DISKBOOT
	LDA BUFEND+1
	STA TEXEND+1
	LDA #$07
	STA TEXBUF+1
	LDA #$1E
	STA BUFEND+1
DISKBOOT RTS
INIT2 JSR HIGHLIGHT
	LDA TEXSTART
	STA CURR
	LDA TEXSTART+1
	STA CURR+1
	JSR REFRESH
	JSR SYSMSG
	LDA # <MSG2
	LDY # >MSG2
	JSR PRMSG
	INC MSGFLG
	JMP CHECK
SYSMSG JSR TOPCLR
	LDA # <MSG1
	LDY # >MSG1
	JSR PRMSG
	LDA #0
	STA MSGFLG
	RTS
TOPCLR LDY #39
	LDA #SPACE
TOPLOOP STA ($58),Y
	DEY
	BPL TOPLOOP
	LDA #0
	STA 82
	STA 85
	STA 84
	RTS
ASTOIN PHA
	AND #128
	STA TEMP
	PLA
	AND #127
	CMP #96
	BCS LOWR
	CMP #32
	BCS NOTCTRL
	CLC
	ADC #64
	JMP LOWR
NOTCTRL SEC
	SBC #32
LOWR ORA TEMP
	RTS
MAIN LDY #0
	STY BLINK
	LDA (CURR),Y
	STA UNDERCURS
MAIN2 LDY #0
	STY SELFLAG
	LDA (CURR),Y
	EOR #$80
	STA (CURR),Y
	LDA BLINK
	EOR #1
	STA BLINK
	JSR REFRESH
WAIT JSR GETIN
	BNE KEYPRESS
	LDA #8
	STA 53279
	LDA 53279
	CMP #6
	BNE FLIPIT
	LDY #0
	STY BLINK
	LDA UNDERCURS
	STA (CURR),Y
	JSR HOME
	JMP MAIN
FLIPIT LDA 20
	AND #16
	BEQ WAIT
	LDA #0
	STA 20
	JMP MAIN2
KEYPRESS TAX
	LDA #8
	STA 53279
	LDA 53279
	CMP #5
	BNE NOTSEL
	LDA #128
	STA SELFLAG
NOTSEL LDY #0
	LDA UNDERCURS
	STA (CURR),Y
NOTBKS LDA MSGFLG
	BEQ NOMSG
	TXA
	PHA
	JSR SYSMSG
	PLA
	TAX
NOMSG TXA
	CMP #155
	BNE NOTCR
	LDX #30
	JMP OVERCTRL
NOTCR TXA
	BIT ESCFLAG
	BMI OVERCTRL
	CMP #156
	BCS CONTROL
	AND #127
	CMP #32
	BCC CONTROL
	CMP #123
	BCS CONTROL
	CMP #92
	BEQ CONTROL
	CMP #94
	BEQ CONTROL
	CMP #95
	BEQ CONTROL
OVERCTRL TXA
	PHA
	LDY #0
	STY ESCFLAG
	LDA (CURR),Y
	CMP #RETCHAR
	BEQ DOINS
	LDA INSMODE
	BEQ NOINST
DOINS JSR INSCHAR
NOINST PLA
	JSR ASTOIN
	AND #127
	ORA SELFLAG
	LDY #0
	STA (CURR),Y
	JSR REFRESH
	SEC
	LDA CURR
	SBC LASTLINE
	STA TEMP
	LDA CURR+1
	SBC LASTLINE+1
	ORA TEMP
	BCC INKURR
	LDA CURR
	ADC #0
	STA LASTLINE
	LDA CURR+1
	ADC #0
	STA LASTLINE+1
INKURR INC CURR
	BNE NOINC2
	INC CURR+1
NOINC2 JSR CHECK
	JMP MAIN
CONTROL LDX CTBL
SRCH CMP CTBL,X
	BEQ FOUND
	DEX
	BNE SRCH
	JMP MAIN
FOUND DEX
	TXA
	ASL A
	TAX
	LDA # >(MAIN-1)
	PHA
	LDA # <(MAIN-1)
	PHA
	LDA VECT+1,X
	PHA
	LDA VECT,X
	PHA
	RTS
CTBL .BYTE 35
	.BYTE 31,30,92,94,2,20,28,29
	.BYTE 126,255,4
	.BYTE 9,125,124,95,5,12,19
	.BYTE 13,18,24,26,16
	.BYTE 254,1,11,6,21,127,157
	.BYTE 3,7,156,27,15
VECT .WORD RIGHT-1,LEFT-1,WLEFT-1,WRIGHT-1,BORDER-1,LETTERS-1
	.WORD SLEFT-1,SRIGHT-1,DELCHAR-1,INSCHAR-1,DELETE-1
	.WORD INSTGL-1,CLEAR-1,PARIGHT-1,PARLEFT-1
	.WORD ERAS-1,TLOAD-1,TSAVE-1
	.WORD DOS-1,INSBUFFER-1,SWITCH-1
	.WORD ENDTEX-1,PRINT-1
	.WORD DELIN-1,ALPHA-1,KILLBUFF-1,HUNT-1,FREEMEM-1,TAB-1
	.WORD LOTTASPACE-1,REPSTART-1,SANDR-1,EATSPACE-1,ESC-1,ONOFF-1
ESC LDA ESCFLAG
	EOR #128
	STA ESCFLAG
	RTS
ONOFF LDA $2204
	EOR #16
	STA $2204
	RTS
CHECK JSR CHECK2
	SEC
	LDA CURR
	SBC TOPLIN
	LDA CURR+1
	SBC TOPLIN+1
	BCS OK1
	SEC
	LDA TOPLIN
	SBC TEXSTART
	STA TEMP
	LDA TOPLIN+1 ;Deze regel sttat niet in't boek!!!!
	SBC TEXSTART+1
	ORA TEMP
	BEQ OK1
	LDA CURR
	STA TOPLIN
	LDA CURR+1
	STA TOPLIN+1
	JSR REFRESH
OK1 SEC
	LDA BOTSCR
	SBC CURR
	STA TEX
	LDA BOTSCR+1
	SBC CURR+1
	STA TEX+1
	ORA TEX
	BEQ EQA
	BCS OK2
EQA CLC
	LDA TOPLIN
	ADC LENTABLE
	STA TOPLIN
	LDA TOPLIN+1
	ADC #0
	STA TOPLIN+1
REF JSR REFRESH
	JMP OK1
OK2 RTS
CHECK2 SEC
	LDA LASTLINE
	SBC TEXEND
	STA TEMP
	LDA LASTLINE+1
	SBC TEXEND+1
	ORA TEMP
	BCC CK3
	LDA TEXEND
	STA LASTLINE
	LDA TEXEND+1
	STA LASTLINE+1
CK3 SEC
	LDA CURR
	SBC TEXSTART
	STA TEMP
	LDA CURR+1
	SBC TEXSTART+1
	ORA TEMP
	BCS INRANGE
	LDA TEXSTART
	STA CURR
	LDA TEXSTART+1
	STA CURR+1
	RTS
INRANGE SEC
	LDA CURR
	SBC LASTLINE
	STA TEMP
	LDA CURR+1
	SBC LASTLINE+1
	ORA TEMP
	BCS OUTRANGE
	RTS
OUTRANGE LDA LASTLINE
	STA CURR
	LDA LASTLINE+1
	STA CURR+1
	RTS
RIGHT
	LDA #8
	STA 53279
	LDA 53279
	CMP #3
	BNE CRIGHT
	LDA LINELEN
	CMP #40
	BEQ NOBIGGER
	INC LINELEN
	INC LINELEN
	DEC RLM
	JSR REFRESH
	JSR CHECK
	LDA #125
	JSR CHROUT
NOBIGGER JMP SYSMSG
CRIGHT INC CURR
	BNE NOINCR
	INC CURR+1
NOINCR JMP CHECK
LEFT LDA #8
	STA 53279
	LDA 53279
	CMP #3
	BNE CLEFT
	LDA LINELEN
	CMP #2
	BEQ TOOSMALL
	DEC LINELEN
	DEC LINELEN
	INC RLM
	JSR REFRESH
	JSR CHECK
	LDA #125
	JSR CHROUT
TOOSMALL JMP SYSMSG
CLEFT LDA CURR
	BNE NODEC
	DEC CURR+1
NODEC DEC CURR
	JMP CHECK
WLEFT LDA CURR
	STA TEX
	LDA CURR+1
	STA TEX+1
	DEC TEX+1
	LDY #$FF
STRIP LDA (TEX),Y
	CMP #SPACE
	BEQ STRLOOP
	CMP #RETCHAR
	BNE WLOOP
STRLOOP DEY
	BNE STRIP
WLOOP LDA (TEX),Y
	CMP #SPACE
	BEQ WROUT
	CMP #RETCHAR
	BEQ WROUT
	DEY
	BNE WLOOP
	RTS
WROUT SEC
	TYA
	ADC TEX
	STA CURR
	LDA TEX+1
	ADC #0
	STA CURR+1
	JMP CHECK
WRIGHT LDY #0
RLOOP LDA (CURR),Y
	CMP #SPACE
	BEQ ROUT
	CMP #RETCHAR
	BEQ ROUT
	INY
	BNE RLOOP
	RTS
ROUT INY
	BNE OIDS
	INC CURR+1
	LDA CURR+1
	CMP LASTLINE+1
	BCC OIDS
	BNE LASTWORD
OIDS LDA (CURR),Y
	CMP #SPACE
	BEQ ROUT
	CMP #RETCHAR
	BEQ ROUT
ADYCURR CLC
	TYA
	ADC CURR
	STA CURR
	LDA CURR+1
	ADC #0
	STA CURR+1
WRTN JMP CHECK
LASTWORD LDA LASTLINE
	STA CURR
	LDA LASTLINE+1
	STA CURR+1
	JMP CHECK
ENDTEX LDA #0
	STA TOPLIN
	LDA LASTLINE+1
	SEC
	SBC #4
	CMP TEXSTART+1
	BCS SAFE
	LDA TEXSTART+1
SAFE STA TOPLIN+1
	JSR REFRESH
	JMP LASTWORD
BORDER INC SCRCOL
	INC SCRCOL
	RTS
SCRCOL .BYTE 8
LETTERS INC TEXCOLR
	INC TEXCOLR
	LDA TEXCOLR
	AND #15
	STA TEXCOLR
	RTS
TEXCOLR .BYTE 2
SLEFT LDA CURR
	STA TEX
	LDA CURR+1
	STA TEX+1
	DEC TEX+1
	LDY #$FF
PMANY LDA (TEX),Y
	CMP #'.-32
	BEQ PSRCH
	CMP #'!-32
	BEQ PSRCH
	CMP #'?-32
	BEQ PSRCH
	CMP #RETCHAR
	BNE PSLOOP
PSRCH DEY
	BNE PMANY
	RTS
PSLOOP LDA (TEX),Y
	CMP #'.-32
	BEQ PUNCT
	CMP #'!-32
	BEQ PUNCT
	CMP #'?-32
	BEQ PUNCT
	CMP #RETCHAR
	BEQ PUNCT
	DEY
	BNE PSLOOP
	DEC TEX+1
	LDA TEX+1
	CMP TEXSTART
	BCS PSLOOP
	JMP FIRSTWORD
PUNCT STY TEMP
	DEC TEMP
SKIPSPC INY
	BEQ REPEAT
	LDA (TEX),Y
	CMP #SPACE
	BEQ SKIPSPC
	DEY
	JMP WROUT
REPEAT LDY TEMP
	JMP PSLOOP
FIRSTWORD LDA TEXSTART
	STA CURR
	LDA TEXSTART+1
	STA CURR+1
	JMP CHECK
SRIGHT LDY #0
SRLP LDA (CURR),Y
	CMP #'.-32
	BEQ PUNCT2
	CMP #'!-32
	BEQ PUNCT2
	CMP #'?-32
	BEQ PUNCT2
	CMP #RETCHAR
	BEQ PUNCT2
	INY
	BNE SRLP
	INC CURR+1
	LDA CURR+1
	CMP LASTLINE+1
	BEQ SRLP
	BCC SRLP
SREXIT JMP LASTWORD
PUNCT2 INY
	BNE NOFIXCURR
	INC CURR+1
	LDA CURR+1
	CMP LASTLINE+1
	BCC NOFIXCURR
	BEQ NOFIXCURR
	JMP LASTWORD
NOFIXCURR LDA (CURR),Y
	CMP #SPACE
	BEQ PUNCT2
	CMP #'.-32
	BEQ PUNCT2
	CMP #'!-32
	BEQ PUNCT2
	CMP #'?-32
	BEQ PUNCT2
	CMP #RETCHAR
	BEQ PUNCT2
	JMP ADYCURR
KILLBUFF LDA TEXBUF
	STA TPTR
	LDA TEXBUF+1
	STA TPTR+1
	JSR TOPCLR
	LDA # <KILLMSG
	LDY # >KILLMSG
	JSR PRMSG
	LDA #1
	STA MSGFLG
	RTS
DEL1 SEC
	LDA CURR
	SBC TEXSTART
	STA TEMP
	LDA CURR+1
	SBC TEXSTART+1
	ORA TEMP
	BNE DEL1A
DELABORT PLA
	PLA
	RTS
DEL1A LDA CURR
	STA FROML
	LDA CURR+1
	STA FROMH
	RTS
DEL2 SEC
	LDA CURR
	STA DESTL
	EOR #$FF
	ADC FROML
	STA GOBLEN
	LDA CURR+1
	STA DESTH
	EOR #$FF
	ADC FROMH
	STA GOBLEN+1
DELC LDA FROML
	STA FROMSAV
	LDA FROMH
	STA FROMSAV+1
	LDA DESTL
	STA DESTSAV
	STA FROML
	LDA DESTH
	STA DESTSAV+1
	STA FROMH
	SEC
	LDA GOBLEN+1
	ADC TPTR+1
	CMP BUFEND+1
	BCC GOSAV
	JSR TOPCLR
	LDA # <BUFERR
	LDY # >BUFERR
	JSR PRMSG
	LDA #1
	STA MSGFLG
	RTS
GOSAV LDA TPTR
	STA DESTL
	LDA TPTR+1
	STA DESTH
	LDA GOBLEN
	STA LLEN
	CLC
	ADC TPTR
	STA TPTR
	LDA GOBLEN+1
	STA HLEN
	ADC TPTR+1
	STA TPTR+1
	JSR UMOVE
	LDA FROMSAV
	STA FROML
	LDA FROMSAV+1
	STA FROMH
	LDA DESTSAV
	STA DESTL
	LDA DESTSAV+1
	STA DESTH
	SEC
	LDA LASTLINE
	SBC DESTL
	STA LLEN
	LDA LASTLINE+1
	SBC DESTH
	STA HLEN
	JSR UMOVE
	SEC
	LDA LASTLINE
	SBC GOBLEN
	STA LASTLINE
	LDA LASTLINE+1
	SBC GOBLEN+1
	STA LASTLINE+1
	RTS
DELCHAR JSR DEL1
	JSR LEFT
	JSR DEL2
FIXTP SEC
	LDA TPTR
	SBC #1
	STA TPTR
	LDA TPTR+1
	SBC #0
	STA TPTR+1
	RTS
DELIN JSR RIGHT
	JSR DEL1
	JSR LEFT
	JSR DEL2
	JMP FIXTP
DELETE JSR KILLBUFF
	LDA #RED
	STA WINDCOLR
	JSR TOPCLR
	LDA # <DELMSG
	LDY # >DELMSG
	JSR PRMSG
	JSR GETAKEY
	PHA
	JSR SYSMSG
	PLA
	AND #95
	ORA #64
	CMP #'W
	BNE NOTWORD
DELWORD JSR DEL1
	JSR WLEFT
	JMP DEL2
NOTWORD CMP #'S
	BNE NOTSENT
DELSENT JSR DEL1
	JSR SLEFT
	JMP DEL2
NOTSENT CMP #'P
	BNE NOTPAR
	JSR DEL1
	JSR PARLEFT
	JMP DEL2
NOTPAR RTS
HOME SEC
	LDA CURR
	SBC TOPLIN
	STA TEMP
	LDA CURR+1
	SBC TOPLIN+1
	ORA TEMP
	BEQ TOPHOME
	LDA TOPLIN
	STA CURR
	LDA TOPLIN+1
	STA CURR+1
WAITST LDA #0
	STA 20
	STA 53279
HOMEPAUSE LDA 20
	CMP #30
	BNE HOMEPAUSE
OUTHOME JMP CHECK
TOPHOME LDA TEXSTART
	STA CURR
	LDA TEXSTART+1
	STA CURR+1
	JMP WAITST
EATSPACE LDA CURR
	STA TEX
	STA DESTL
	LDA CURR+1
	STA TEX+1
	STA DESTH
	LDY #0
SPCSRCH LDA (TEX),Y
	CMP #SPACE
	BNE OUTSPACE
	INY
	BNE SPCSRCH
	LDA TEX+1
	CMP LASTLINE+1
	BCC GOINC
	LDA LASTLINE
	STA TEX
	LDA LASTLINE+1
	STA TEX+1
	LDY #0
	JMP OUTSPACE
GOINC INC TEX+1
	JMP SPCSRCH
OUTSPACE CLC
	TYA
	ADC TEX
	STA FROML
	LDA #0
	ADC TEX+1
	STA FROMH
	SEC
	LDA LASTLINE
	SBC DESTL
	STA LLEN
	LDA LASTLINE+1
	SBC DESTH
	STA HLEN
	SEC
	LDA FROML
	SBC DESTL
	STA GOBLEN
	LDA FROMH
	SBC DESTH
	STA GOBLEN+1
	JSR UMOVE
	SEC
	LDA LASTLINE
	SBC GOBLEN
	STA LASTLINE
	LDA LASTLINE+1
	SBC GOBLEN+1
	STA LASTLINE+1
	RTS
LOTTASPACE LDA #255
	STA INSLEN
	JMP TAB2
TAB LDA #5
	STA INSLEN
	JSR TAB2
	LDA (CURR),Y
	CMP #SPACE
	BNE NOINCY
	INY
NOINCY JMP ADYCURR
TAB2 LDA #0
	STA INSLEN+1
	JSR INSBLOCK
	LDA #SPACE
	LDX INSLEN
	LDY #0
FILLSP STA (CURR),Y
	INY
	DEX
	BNE FILLSP
	RTS
INSCHAR LDA #1
	STA INSLEN
	LDA #0
	STA INSLEN+1
	JSR INSBLOCK
	LDA #SPACE
	LDY #0
	STA (CURR),Y
	JMP CHECK
INSBLOCK CLC
	LDA LASTLINE
	ADC INSLEN
	LDA LASTLINE+1
	ADC INSLEN+1
	CMP TEXEND+1
	BCC OKINS
	PLA
	PLA
	JMP INOUT
OKINS CLC
	LDA CURR
	STA FROML
	ADC INSLEN
	STA DESTL
	LDA CURR+1
	STA FROMH
	ADC INSLEN+1
	STA DESTH
	SEC
	LDA LASTLINE
	SBC FROML
	STA LLEN
	LDA LASTLINE+1
	SBC FROMH
	STA HLEN
	JSR DMOVE
	CLC
	LDA LASTLINE
	ADC INSLEN
	STA LASTLINE
	LDA LASTLINE+1
	ADC INSLEN+1
	STA LASTLINE+1
INOUT RTS
INSTGL LDA INSMODE
	EOR #BLUE
	STA INSMODE
	RTS
YORN LDA # <YMSG
	LDY # >YMSG
	JSR PRMSG
YORNKEY JSR GETIN
	AND #127
	BEQ YORNKEY
	CMP #125
	BEQ YORNKEY
	AND #223
	CMP #'Y
	RTS
CLEAR LDA #8
	STA 53279
	LDA 53279
	CMP #3
	BEQ OKCLEAR
	RTS
OKCLEAR LDA #RED
	STA WINDCOLR
	JSR TOPCLR
	LDA # <CLRMSG
	LDY # >CLRMSG
	JSR PRMSG
	JSR YORN
	BEQ DOIT
	JMP SYSMSG
DOIT LDX #$FA
	TXS
	JSR ERASE
	JSR INIT2
	JMP MAIN
PARIGHT LDY #0
PARLP LDA (CURR),Y
	CMP #RETCHAR
	BEQ RETFOUND
	INY
	BNE PARLP
	INC CURR+1
	LDA CURR+1
	CMP LASTLINE+1
	BCC PARLP
	BEQ PARLP
	JMP LASTWORD
RETFOUND INY
	BNE GOADY
	INC CURR+1
GOADY JMP ADYCURR
PARLEFT LDA CURR
	STA TEX
	LDA CURR+1
	STA TEX+1
	DEC TEX+1
	LDY #$FF
PARLOOP LDA (TEX),Y
	CMP #RETCHAR
	BEQ RETF2
PARCONT DEY
	CPY #255
	BNE PARLOOP
	DEC TEX+1
	LDA TEX+1
	CMP TEXSTART+1
	BCS PARLOOP
	JMP FIRSTWORD
RETF2 SEC
	TYA
	ADC TEX
	STA TEX
	LDA #0
	ADC TEX+1
	STA TEX+1
	SEC
	LDA TEX
	SBC CURR
	STA TEMP
	LDA TEX+1
	SBC CURR+1
	ORA TEMP
	BNE TEXTOCURR
	STY TEMP
	CLC
	LDA TEX
	SBC TEMP
	STA TEX
	LDA TEX+1
	SBC #0
	STA TEX+1
	JMP PARCONT
TEXTOCURR LDA TEX
	STA CURR
	LDA TEX+1
	STA CURR+1
	JMP CHECK
HIGHLIGHT LDA #64
	STA $D40E
	LDA # <DLI
	STA $0200
	LDA # >DLI
	STA $0201
	LDA 560
	STA TEMP
	LDA 561
	STA TEMP+1
	LDY #0
DLOOP LDA DLIST,Y
	STA (TEMP),Y
	INY
	CPY #28
	BNE DLOOP
	LDY #4
	LDA $58
	STA (TEMP),Y
	LDA $59
	INY
	STA (TEMP),Y
	LDY #26
	LDA TEMP
	STA (TEMP),Y
	LDA TEMP+1
	INY
	STA (TEMP),Y
	LDA #$C0
	STA $D40E
	RTS
DLIST .BYTE 112,112,112,3+64+128,0,0
	.BYTE 3,3,3,3,3,3,3,3,3,3,3,3,3
	.BYTE 3,3,3,3,3,16,65,0,0
DLI PHA
	LDA SCRCOL
	STA $D40A
	STA $D018
	STA 712
	LDA TEXCOLR
	STA $D017
	LDA WINDCOLR
	STA 710
	LDA #10
	STA 709
	LDA #$20
	STA 756
	LDA #0
	STA $02B6
	PLA
	RTI
ERAS LDA #8
	STA 53279
	LDA 53279
	CMP #3
	BEQ ERAS1
	JSR KILLBUFF
ERAS1 JSR TOPCLR
	LDA # <ERASMSG
	LDY # >ERASMSG
	JSR PRMSG
ERASAGAIN LDY #0
	LDA (CURR),Y
	EOR #$80
	STA (CURR),Y
	JSR REFRESH
	LDY #0
	LDA (CURR),Y
	EOR #$80
	STA (CURR),Y
	LDA #RED
	STA WINDCOLR
	JSR GETAKEY
	AND #95
	ORA #64
	CMP #'W
	BNE NOWORD
ERASWORD JSR ERA1
	JSR WRIGHT
	JMP ERA2
NOWORD CMP #'S
	BNE UNSENT
ERASENT JSR ERA1
	JSR SRIGHT
	JMP ERA2
UNSENT CMP #'P
	BNE NOPAR
	JSR ERA1
	JSR PARIGHT
	JMP ERA2
NOPAR JSR CHECK
	JMP SYSMSG
ERA1 LDA CURR
	STA DESTL
	STA SAVCURR
	LDA CURR+1
	STA DESTH
	STA SAVCURR+1
	RTS
ERA2 SEC
	LDA CURR
	STA FROML
	SBC SAVCURR
	STA GOBLEN
	LDA CURR+1
	STA FROMH
	SBC SAVCURR+1
	STA GOBLEN+1
	JSR DELC
	LDA SAVCURR
	STA CURR
	LDA SAVCURR+1
	STA CURR+1
	JSR REFRESH
	JMP ERASAGAIN
INPUT LDA #39
	SBC 85
	STA LIMIT
INP1 LDY #0
	STY INLEN
	STY 752
	LDA #32
	JSR CHROUT
	LDA #126
	JSR CHROUT
CURSIN STY INLEN
	JSR GETAKEY
	LDY INLEN
	BIT ESCFLAG
	BMI ESCKEY
	CMP #27
	BNE NOESC
	LDA #128
	STA ESCFLAG
	STA $02A2
	JMP CURSIN
NOESC CMP #155
	BEQ INEXIT
	CMP #126
	BNE NOBACK
	DEY
	BPL NOTZERO
	INY
	JMP CURSIN
NOTZERO LDA #126
	JSR CHROUT
	JMP CURSIN
NOBACK STA TEMP
	AND #127
	CMP #32
	BCC CURSIN
	CMP #125
	BCS CURSIN
	CPY LIMIT
	BEQ CURSIN
	LDA TEMP
ESCKEY AND #127
	LDX #8
	STX 53279
	LDX 53279
	CPX #5
	BNE SKIPSEL
	ORA #128
SKIPSEL STA INBUFF,Y
	JSR CHROUT
	LDA #0
	STA ESCFLAG
	INY
	JMP CURSIN
INEXIT LDX #1
	STX 752
	LDA #0
	STA INBUFF,Y
	TYA
	RTS

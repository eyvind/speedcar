CAST  LDA #0
	STA CONVFLAG
	JMP CAST1
CINTOAS LDA #128
	STA CONVFLAG
CAST1 LDA TEXSTART
	STA TEX
	LDA TEXSTART+1
	STA TEX+1
	JMP CIN
CASTOIN LDA #0
	STA CONVFLAG
	LDA CURR
	STA TEX
	LDA CURR+1
	STA TEX+1
CIN SEC
	LDA LASTLINE+1
	SBC TEX+1
	TAX
	INX
	LDY #0
CVLOOP LDA (TEX),Y
	BIT CONVFLAG
	BMI COTHER
	CMP #155
	BNE NOTRTN
	LDA #RETCHAR
	JMP OVEROTHER
NOTRTN JSR ASTOIN
	JMP OVEROTHER
COTHER CMP #RETCHAR
	BNE NOTRC
	LDA #155
	JMP OVEROTHER
NOTRC JSR INTOAS
OVEROTHER STA (TEX),Y
	INY
	BNE CVLOOP
	INC TEX+1
	DEX
	BNE CVLOOP
	RTS
TSAVE JSR TOPCLR
	LDA # <SAVMSG
	LDY # >SAVMSG
	JSR PRMSG
	LDA #8
	JSR TOPEN
	BMI ERROR
	JSR CINTOAS
	LDX #$70
	LDA TEXSTART
	STA ICBADR,X
	LDA TEXSTART+1
	STA ICBADR+1,X
	SEC
	LDA LASTLINE
	SBC TEXSTART
	STA ICBLEN,X
	LDA LASTLINE+1
	SBC TEXSTART+1
	STA ICBLEN+1,X
	LDA #11
	STA ICCOM,X
	JSR CIO
	BMI ERR1
	JSR CAST
	JSR CLOSE7
	BMI ERROR
	JMP FINE
ERR1 TYA
	PHA
	JSR CAST
	PLA
	TAY
ERROR CPY #128
	BEQ STOPPED
	TYA
	PHA
	LDA #125
	JSR CHROUT
	LDA # <ERRMSG
	LDY # >ERRMSG
	JSR PRMSG
	PLA
	TAX
	LDA #0
	JSR OUTNUM
ERXIT JSR IOCLOSE
	JSR HIGHLIGHT
	LDA #1
	STA MSGFLG
	RTS
STOPPED JSR TOPCLR
	LDA # <BRMSG
	LDY # >BRMSG
	JSR PRMSG
	JMP ERXIT
IOCLOSE LDX IOCB
	LDA #12
	STA ICCOM,X
	JMP CIO
TOPEN LDX #$70
	STX IOCB
	STA ACCESS
	LDA SHFLOK
	PHA
	LDA #64
	STA SHFLOK
	JSR INPUT
	PLA
	STA SHFLOK
	LDA INLEN
	BNE OPCONT
OPABORT JSR SYSMSG
	PLA
	PLA
	JMP HIGHLIGHT
OPCONT JSR IOCLOSE
	LDX IOCB
	LDA # <INBUFF
	STA ICBADR,X
	LDA # >INBUFF
	STA ICBADR+1,X
	LDA INLEN
	STA ICBLEN,X
	LDA #0
	STA ICBLEN+1,X
	LDA ACCESS
	STA ICAUX1,X
	LDA #3
	STA ICCOM,X
	JMP CIO
TLOAD SEC
	LDA CURR
	SBC TEXSTART
	STA TEX
	STA INDIR
	LDA CURR+1
	SBC TEXSTART+1
	STA TEX+1
	STA INDIR+1
	ORA TEX
	BEQ LOAD2
	LDA #$C4
	STA WINDCOLR
LOAD2 JSR TOPCLR
	LDA # <LOADMSG
	LDY # >LOADMSG
	JSR PRMSG
	LDA #4
	JSR TOPEN
	BPL OKLOD
GOERROR JMP ERROR
OKLOD LDA WINDCOLR
	CMP #$C4
	BEQ NOER
	JSR ERASE
NOER JSR LOADLINK
	CPY #128
	BCC JFINE
	JMP ERROR
JFINE JMP FINE
LOADLINK LDX IOCB
	LDA CURR
	STA ICBADR,X
	LDA CURR+1
	STA ICBADR+1,X
	SEC
	LDA TEXEND
	SBC CURR
	STA ICBLEN,X
	LDA TEXEND+1
	SBC CURR+1
	STA ICBLEN+1,X
	LDA #7
	STA ICCOM,X
	JSR CIO
	BPL TEXOK
	CPY #136
	BEQ TEXOK
	RTS
TEXOK LDX IOCB
	CLC
	LDA ICBLEN,X
	ADC TEXSTART
	STA LASTLINE
	LDA ICBLEN+1,X
	ADC TEXSTART+1
	STA LASTLINE+1
	CLC
	LDA LASTLINE
	ADC INDIR
	STA LASTLINE
	LDA LASTLINE+1
	ADC INDIR+1
	STA LASTLINE+1
	JSR CASTOIN
	LDA LASTLINE
	STA TEX
	LDA LASTLINE+1
	STA TEX+1
	LDA #0
	TAY
NOGARBAGE STA (TEX),Y
	INY
	BNE NOGARBAGE
	RTS
FINE JSR IOCLOSE
	BPL PROKMSG
	JMP ERROR
PROKMSG LDA #125
	JSR CHROUT
	LDA # <OKMSG
	LDY # >OKMSG
	JSR PRMSG
	JMP ERXIT
DELITE LDA #$40
	STA $D40E
	LDA SCRCOL
	STA 710
	STA 712
	LDA TEXCOLR
	STA 709
	RTS
ASCHEX LDX #0
	STX BCD
	STX BCD+1
	STX HEX
	STX HEX+1
DIGIT SEC
	LDA (TEX),Y
	SBC #16
	BCC NONUM
	CMP #10
	BCS NONUM
	ASL BCD
	ROL BCD+1
	ASL BCD
	ROL BCD+1
	ASL BCD
	ROL BCD+1
	ASL BCD
	ROL BCD+1
	ORA BCD
	STA BCD
	INY
	BNE DIGIT
	INC TEX+1
	JMP DIGIT
NONUM SED
DECHEX LDA BCD
	ORA BCD+1
	BEQ DONENUM
	SEC
	LDA BCD
	SBC #1
	STA BCD
	LDA BCD+1
	SBC #0
	STA BCD+1
	INC HEX
	BNE NOHEXINC
	INC HEX+1
NOHEXINC JMP DECHEX
DONENUM LDA HEX
	CLD
	RTS
INSBUFFER SEC
	LDA TPTR
	SBC TEXBUF
	STA BUFLEN
	LDA TPTR+1
	SBC TEXBUF+1
	STA BUFLEN+1
	ORA BUFLEN
	BNE OKBUFF
	JSR TOPCLR
	LDA # <INSMSG
	LDY # >INSMSG
	JSR PRMSG
	LDA #1
	STA MSGFLG
	RTS
OKBUFF CLC
	LDA CURR
	STA FROML
	ADC BUFLEN
	STA DESTL
	LDA CURR+1
	STA FROMH
	ADC BUFLEN+1
	STA DESTH
	SEC
	LDA LASTLINE
	SBC FROML
	STA LLEN
	LDA LASTLINE+1
	SBC FROMH
	STA HLEN
	CLC
	ADC DESTH
	CMP TEXEND+1
	BCC OKMOV
	JSR TOPCLR
	LDA # <INSERR
	LDY # >INSERR
	JSR PRMSG
	LDA #1
	STA MSGFLG
	RTS
OKMOV JSR DMOVE
	CLC
	LDA BUFLEN
	STA LLEN
	ADC LASTLINE
	STA LASTLINE
	LDA BUFLEN+1
	STA HLEN
	ADC LASTLINE+1
	STA LASTLINE+1
	LDA CURR
	STA DESTL
	LDA CURR+1
	STA DESTH
	LDA TEXBUF
	STA FROML
	LDA TEXBUF+1
	STA FROMH
	JSR UMOVE
	JMP CHECK
SWITCH LDY #0
	LDA (CURR),Y
	TAX
	INY
	LDA (CURR),Y
	DEY
	STA (CURR),Y
	INY
	TXA
	STA (CURR),Y
	RTS
ALPHA LDY #0
	LDA (CURR),Y
	AND #63
	CMP #33
	BCC NOTALPHA
	CMP #59
	BCS NOTALPHA
	LDA (CURR),Y
	EOR #64
	STA (CURR),Y
NOTALPHA JMP RIGHT
INTOAS PHA
	AND #128
	STA TEMP
	PLA
	AND #127
	CMP #96
	BCS XINT
INCONT CMP #64
	BCC INT1
	SBC #64
	JMP XINT
INT1 ADC #32
XINT ORA TEMP
	RTS
DEFTAB .BYTE 5,75,66,5,58,1,1,1,0,1,0,80
PRCODES .BYTE 27,14,15,18
PCHROUT STA PCR
	TXA
	PHA
	TYA
	PHA
	SEC
	LDA PAGENUM
	SBC STARTNUM
	LDA PAGENUM+1
	SBC STARTNUM+1
	BCC SKIPOUT
	LDA #1
	STA 766
	LDX #$70
	LDA #0
	STA ICBLEN,X
	STA ICBLEN+1,X
	LDA #11
	STA ICCOM,X
	LDA PCR
	JSR CIO
	PHP
	LDA #0
	STA 766
	PLP
	BPL SHIFTFREEZE
ERRLINK JSR ERROR
	LDX #$FA
	TXS
	JMP MAIN
SHIFTFREEZE LDA $02FF ;CTRL-1
	BNE SHIFTFREEZE
SKIPOUT PLA
	TAY
	PLA
	TAX
	LDA PCR
	RTS
PRIN JSR TOPCLR
	LDA # <PRINMSG
	LDY # >PRINMSG
	JMP PRMSG
PBORT JMP PEXIT
PRINT JSR TOPCLR
	LDA # <FNMSG
	LDY # >FNMSG
	JSR PRMSG
	JSR DELITE
	LDA #8
	JSR TOPEN
	BPL PROK
	JMP PEXIT
PROK JSR DELITE
	JSR PRIN
	LDX #0
	STX FTLEN
	STX HDLEN
	STX NEEDASC
	STX UNDERLINE
	STX LINEFEED
COPYDEF LDA DEFTAB,X
	STA LMARGIN,X
	INX
	CPX #12
	BNE COPYDEF
	LDA #$FF
	STA LINE
	STA NOMARG
	LDX #4
COPYDEFS LDA PRCODES-1,X
	STA CODEBUFFER+16,X
	DEX
	BNE COPYDEFS
RETEX LDA TEXSTART
	STA TEX
	LDA TEXSTART+1
	STA TEX+1
PLOOP LDY #0
	STY POS
	CPY NOMARG
	BEQ PLOOP1
	LDA LMARGIN
	STA POS
PLOOP1 LDA (TEX),Y
	BPL NOTSP
	JMP SPECIAL
NOTSP CMP #RETCHAR
	BEQ FOUNDSPACE
NOTRET STA PRBUFF,Y
	INY
	INC POS
	LDA POS
	CMP RMARGIN
	BCC PLOOP1
	STY FINPOS
FINDSPACE LDA (TEX),Y
	CMP #SPACE
	BEQ FOUNDSPACE
	DEC POS
	DEY
	BNE FINDSPACE
	LDY FINPOS
FSPACE INY
	LDA (TEX),Y
	CMP #SPACE
	BEQ FOUNDSPACE
	DEY
FOUNDSPACE STY FINPOS
OVERSTOR TYA
	SEC
	ADC TEX
	STA TEX
	LDA TEX+1
	ADC #0
	STA TEX+1
	LDY #0
DOBUFF LDA LINE
	CMP #$FF
	BNE DOBUF2
	JSR TOP
DOBUF2 LDA NOMARG
	BEQ OVERMARG
	JSR LMARG
OVERMARG SEC
	ROL NOMARG
	LDA FINPOS
	STA ENDPOS
	LDA # <PRBUFF
	STA INDIR
	LDA # >PRBUFF
	STA INDIR+1
	JSR BUFPRT
ZBUFF JSR CRLF
	LDA LINE
	CMP BOTMARG
	BCC NOTPAGE
	JSR PAGE
NOTPAGE SEC
	LDA TEX
	SBC LASTLINE
	STA TEMP
	LDA TEX+1
	SBC LASTLINE+1
	ORA TEMP
	BEQ DORPT
	BCC DORPT
	LDA FTLEN
	BEQ PXIT
	LDA #0
	STA HDLEN
	STA TOPMARG
	JSR PAGE
PXIT LDA INBUFF
	CMP #'E
	BNE PEXIT
	LDA #155
	JSR CHROUT
	LDA # <DIRMSG
	LDY # >DIRMSG
	JSR PRMSG
	JSR GETAKEY
PEXIT JSR CLOSE7
	LDX #$FA
	TXS
	JSR HIGHLIGHT
	LDA #125
	JSR CHROUT
	JSR SYSMSG
	JMP MAIN
DORPT JMP PLOOP
PAGE SEC
	LDA PAGELENGTH
	SBC LINE
	TAY
	DEY
	DEY
	BEQ NOSK
	BMI NOSK
NEXPAGE JSR CR
	DEY
	BNE NEXPAGE
NOSK LDA FTLEN
	BEQ SKIPFT
	STA ENDPOS
	LDA # <FTBUFF
	STA INDIR
	LDA # >FTBUFF
	STA INDIR+1
	JSR LMARG
	JSR BUFPRT
SKIPFT JSR CR
	JSR CR
	JSR CR
	INC PAGENUM
	BNE NOIPN
	INC PAGENUM+1
NOIPN LDA CONTINUOUS
	BNE TOP
	SEC
	LDA PAGENUM
	SBC STARTNUM
	LDA PAGENUM+1
	SBC STARTNUM+1
	BCC TOP
	JSR TOPCLR
	LDA # <WAITMSG
	LDY # >WAITMSG
	JSR PRMSG
	JSR GETAKEY
	JSR PRIN
TOP LDA HDLEN
	BEQ NOHEADER
	STA ENDPOS
	LDA # <HDBUFF
	STA INDIR
	LDA # >HDBUFF
	STA INDIR+1
	JSR LMARG
	JSR BUFPRT
NOHEADER LDY TOPMARG
	STY LINE
	DEY
	BEQ SKIPTOP
	BMI SKIPTOP
TOPLP JSR CR
	DEY
	BNE TOPLP
SKIPTOP RTS
LMARG LDA #32
	LDY LMARGIN
	STY POS
	BEQ LMEXIT
LMLOOP JSR PCHROUT
	DEY
	BNE LMLOOP
LMEXIT RTS
CRLF LDY SPACING
	CLC
	TYA
	ADC LINE
	STA LINE
CRLOOP JSR CR
	DEY
	BNE CRLOOP
	RTS
CR  LDA #155
	JSR PCHROUT
	LDA LINEFEED
	BEQ NOLF
	JSR PCHROUT
NOLF RTS
SPECIAL STA SAVCHAR
	AND #127
	JSR INTOAS
	LDX SPTAB
SRCHSP CMP SPTAB,X
	BEQ FSP
	DEX
	BNE SRCHSP
	DEC POS
	JMP DEFINE
FSP DEX
	TXA
	ASL A
	TAX
	STY YSAVE
	LDA # >(SPCONT-1)
	PHA
	LDA # <(SPCONT-1)
	PHA
	LDA SPVECT+1,X
	PHA
	LDA SPVECT,X
	PHA
	RTS
SPCONT SEC
	LDA YSAVE
	ADC TEX
	STA TEX
	LDA TEX+1
	ADC #0
	STA TEX+1
	JMP PLOOP
SPCEXIT LDA (TEX),Y
	CMP #RETCHAR
	BEQ NOAD
	DEY
NOAD STY YSAVE
	RTS
SPTAB .BYTE 17
	.BYTE "wlrtbsnhf@p?xmigj"
SPVECT .WORD PW-1,LM-1,RM-1,TP-1
	.WORD BT-1,SP-1,NX-1,HD-1,FT-1
	.WORD PN-1,PL-1,SPAGE-1,ACROSS-1
	.WORD MRELEASE-1,COMMENT-1,LINK-1
	.WORD LFSET-1
MRELEASE INY
	LDA #0
	STA NOMARG
	JMP SPCEXIT
ACROSS INY
	JSR ASCHEX
	STA PAGEWIDTH
	JMP SPCEXIT
SPAGE INY
	JSR ASCHEX
	STA STARTNUM
	LDA HEX+1
	STA STARTNUM+1
	JMP SPCEXIT
PN  INY
	JSR ASCHEX
	STA PAGENUM
	LDA HEX+1
	STA PAGENUM+1
	JMP SPCEXIT
PL  INY
	JSR ASCHEX
	STA PAGELENGTH
	JMP SPCEXIT
PW  LDA #0
	STA CONTINUOUS
	INY
	JMP SPCEXIT
LFSET LDA #10
	STA LINEFEED
	INY
	JMP SPCEXIT
LM  INY
	JSR ASCHEX
	STA LMARGIN
	JMP SPCEXIT
RM  INY
	JSR ASCHEX
	STA RMARGIN
	JMP SPCEXIT
TP  INY
	JSR ASCHEX
	STA TOPMARG
	JMP SPCEXIT
BT  INY
	JSR ASCHEX
	STA BOTMARG
	JMP SPCEXIT
SP  INY
	JSR ASCHEX
	STA SPACING
	JMP SPCEXIT
NX  LDY YSAVE
	INY
	TYA
	PHA
	JSR PAGE
	PLA
	TAY
	STY YSAVE
	RTS
HD  JSR PASTRET
	DEY
	STY HDLEN
	LDY #1
HDCOPY LDA (TEX),Y
	STA HDBUFF-1,Y
	INY
	CPY HDLEN
	BCC HDCOPY
	BEQ HDCOPY
	INY
	JMP SPCEXIT
PASTRET INY
	LDA (TEX),Y
	CMP #RETCHAR
	BNE PASTRET
	RTS
FT  JSR PASTRET
	DEY
	STY FTLEN
	LDY #1
FTCOPY LDA (TEX),Y
	STA FTBUFF-1,Y
	INY
	CPY FTLEN
	BCC FTCOPY
	BEQ FTCOPY
	JMP SPCEXIT
COMMENT JSR PASTRET
	JMP SPCEXIT
DEFINE INY
	LDA (TEX),Y
	CMP #'=-32
	BEQ DODEFINE
	DEY
	LDA SAVCHAR
	JMP NOTRET
DODEFINE INY
	JSR ASCHEX
	PHA
	LDA SAVCHAR
	AND #127
	TAX
	PLA
	STA CODEBUFFER,X
	JSR SPCEXIT
	JMP SPCONT
LINK LDY #1
	LDX #0
FNCOPY LDA (TEX),Y
	CMP #RETCHAR
	BEQ FNEND
	JSR INTOAS
	STA INBUFF,X
	INY
	INX
	CPX #14
	BNE FNCOPY
FNEND STX INLEN
	LDA #0
	STA INBUFF,X
	LDX #$60
	STX IOCB
	LDA #4
	STA ACCESS
	JSR OPCONT
	BPL LNOERR
	JMP ERRLINK
LNOERR LDA #0
	STA INDIR
	STA INDIR+1
	JSR ERASE
	JSR LOADLINK
	BPL LCONT
	JMP ERRLINK
LCONT PLA
	PLA
	LDX #$70
	STA IOCB
	JMP RETEX
SANDR JSR RESET
	LDA HUNTLEN
	BEQ NOSR
	JSR ASKREP
SNR JSR CONTSRCH
	LDA FPOS+1
	CMP #$FF
	BEQ NOSR
	JSR REPL
	JSR REFRESH
	JMP SNR
NOSR JMP SYSMSG
HUNT LDA #8
	STA 53279
	LDA 53279
	CMP #3
	BNE CONTSRCH
RESET JSR TOPCLR
	LDA # <SRCHMSG
	LDY # >SRCHMSG
	JSR PRMSG
	JSR INPUT
	STA HUNTLEN
	BNE OKSRCH
	JMP SYSMSG
OKSRCH LDY #0
TOBUFF LDA INBUFF,Y
	STA HUNTBUFF,Y
	INY
	CPY INLEN
	BNE TOBUFF
	JMP SYSMSG
CONTSRCH LDA CURR
	STA TEX
	LDA CURR+1
	STA TEX+1
	LDA #$FF
	STA FPOS+1
	LDY #1
SRCH0 LDX #0
	LDA HUNTLEN
	BEQ NOTFOUND
SRCH1 LDA HUNTBUFF,X
	JSR ASTOIN
	CMP (TEX),Y
	BEQ CY
	CPX #0
	BNE SRCH0
	DEX
CY  INY
	BNE NOVFL
	INC TEX+1
	LDA TEX+1
	CMP LASTLINE+1
	BEQ NOVFL
	BCS NOTFOUND
NOVFL INX
	CPX HUNTLEN
	BNE SRCH1
	CLC
	TYA
	ADC TEX
	STA TEMP
	LDA TEX+1
	ADC #0
	STA TEMP+1
	LDA LASTLINE
	CMP TEMP
	LDA LASTLINE+1
	SBC TEMP+1
	BCC NOTFOUND
	SEC
	LDA TEMP
	SBC HUNTLEN
	STA CURR
	STA FPOS
	LDA TEMP+1
	SBC #0
	STA CURR+1
	STA FPOS+1
	JSR CHECK
	RTS
NOTFOUND JSR TOPCLR
	LDA # <NFMSG
	LDY # >NFMSG
	JSR PRMSG
	LDA #1
	STA MSGFLG
	RTS
REPSTART LDA #8
	STA 53279
	LDA 53279
	CMP #3
	BNE REPL
ASKREP JSR TOPCLR
	LDA # <REPMSG
	LDY # >REPMSG
	JSR PRMSG
	JSR INPUT
	STA REPLEN
	BEQ NOREP
	LDY #0
REPMOV LDA INBUFF,Y
	STA REPBUFF,Y
	INY
	CPY INLEN
	BNE REPMOV
NOREP JMP SYSMSG
REPL SEC
	LDA CURR
	STA DESTL
	SBC FPOS
	STA TEMP
	LDA CURR+1
	STA DESTH
	SBC FPOS+1
	ORA TEMP
	BNE NOREPL
	LDA #$FF
	STA FPOS+1
	CLC
	LDA HUNTLEN
	ADC CURR
	STA FROML
	LDA #0
	ADC CURR+1
	STA FROMH
	SEC
	LDA LASTLINE
	SBC DESTL
	STA LLEN
	LDA LASTLINE+1
	SBC DESTH
	STA HLEN
	JSR UMOVE
	SEC
	LDA LASTLINE
	SBC HUNTLEN
	STA LASTLINE
	LDA LASTLINE+1
	SBC #0
	STA LASTLINE+1
	LDA REPLEN
	BEQ NOREPL
	STA INSLEN
	LDA #0
	STA INSLEN+1
	JSR INSBLOCK
	LDY #0
REPLOOP LDA REPBUFF,Y
	JSR ASTOIN
	STA (CURR),Y
	INY
	CPY REPLEN
	BNE REPLOOP
	CLC
	LDA CURR
	ADC REPLEN
	STA CURR
	LDA CURR+1
	ADC #0
	STA CURR+1
NOREPL JMP CHECK
BUFPRT LDY #0
BUFLP CPY ENDPOS
	BEQ ENDBUFF
	LDA (INDIR),Y
	BMI SPEC2
	JSR INTOAS
	JSR PCHROUT
	LDA UNDERLINE
	BEQ NOBRK
	LDA #8
	JSR PCHROUT
	LDA #95
	JSR PCHROUT
NOBRK INY
	JMP BUFLP
ENDBUFF RTS
SPEC2 STY YSAVE
	AND #127
	STA SAVCHAR
	JSR INTOAS
OTHER CMP #'c
	BNE NOTCENTER
	SEC
	LDA PAGEWIDTH
	SBC ENDPOS
	LSR A
	SEC
	SBC LMARGIN
	TAY
	LDA #32
CLOOP JSR PCHROUT
	DEY
	BNE CLOOP
	LDY YSAVE
	JMP NOBRK
NOTCENTER CMP #'e
	BNE NOTEDGE
EDGE SEC
	LDA RMARGIN
	SBC ENDPOS
	SEC
	SBC LMARGIN
	TAY
	LDA #32
	JMP CLOOP
NOTEDGE CMP #'u
	BNE NOTOG
	LDA UNDERLINE
	EOR #1
	STA UNDERLINE
NOTOG CMP #'#
	BNE DOCODES
DOPGN STY YSAVE
	LDX PAGENUM
	LDA PAGENUM+1
	JSR PROUTNUM
	LDY YSAVE
	JMP NOBRK
DOCODES LDX SAVCHAR
	LDA CODEBUFFER,X
	JSR PCHROUT
	JMP NOBRK
FREEMEM JSR TOPCLR
	SEC
	LDA TEXEND
	SBC LASTLINE
	TAX
	LDA TEXEND+1
	SBC LASTLINE+1
	JSR OUTNUM
	LDA #1
	STA MSGFLG
	RTS

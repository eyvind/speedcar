; This is the main input/output portion
; of SpeedScript, responsible for loading,
; saving, and all printing functions.

; CAST and CINSTOAS (standing for Convert
; to ASCII and Convert INTernal code to
; ASCII) translate the way SpeedScript
; stores text in memory (internal screen
; codes) into ASCII so that disk files
; will be compatible with most other
; software. In addition, the return- mark
; is changed to character 155, and vice
; versa. This is why you can't load a
; machine language file into SpeedScript,
; edit it, then save it back as a
; runnable modification. All back-arrows
; are turned into carriage returns on
; output, and all carriage returns
; (155's) are turned into back-arrows
; (30's) on input.
CAST  LDA #0
	STA CONVFLAG
	JMP CAST1
CINTOAS LDA #128
	STA CONVFLAG
CAST1 LDA TEXSTART
	STA TEX
	LDA TEXSTART+1
	STA TEX+1
	JMP CIN
CASTOIN LDA #0
	STA CONVFLAG
	LDA CURR
	STA TEX
	LDA CURR+1
	STA TEX+1
CIN SEC
	LDA LASTLINE+1
	SBC TEX+1
	TAX
	INX
	LDY #0
CVLOOP LDA (TEX),Y
	BIT CONVFLAG
	BMI COTHER
	CMP #155
	BNE NOTRTN
	LDA #RETCHAR
	JMP OVEROTHER
NOTRTN JSR ASTOIN
	JMP OVEROTHER
COTHER CMP #RETCHAR
	BNE NOTRC
	LDA #155
	JMP OVEROTHER
NOTRC JSR INTOAS
OVEROTHER STA (TEX),Y
	INY
	BNE CVLOOP
	INC TEX+1
	DEX
	BNE CVLOOP
	RTS

; Here is where most of the input/output
; routines start. TSAVE saves the entire
; document area using the CIO block
; output routine (PUT TEXT). TOPEN is
; called by both TSAVE and TLOAD to get
; the filename and open the file. The
; device specification (D: or C:) must be
; typed in by the user.

; TSAVE prints the Save: prompt, goes to
; TOPEN with an 8 (for output, the same
; number in OPEN 1,8,0,"D:file"), and
; uses IOCB #7 (LDX #$70) to send a PUT
; TEXT command (11). Text is written from
; the start-of-text with a length of
; LASTLINE - TEXSTART.
TSAVE JSR TOPCLR
	LDA # <SAVMSG
	LDY # >SAVMSG
	JSR PRMSG
	LDA #8
	JSR TOPEN
	BMI ERROR
	JSR CINTOAS
	LDX #$70
	LDA TEXSTART
	STA ICBADR,X
	LDA TEXSTART+1
	STA ICBADR+1,X
	SEC
	LDA LASTLINE
	SBC TEXSTART
	STA ICBLEN,X
	LDA LASTLINE+1
	SBC TEXSTART+1
	STA ICBLEN+1,X
	LDA #11
	STA ICCOM,X
	JSR CIO
	; The N (negative) bit is set when an
	; error occurs after a call to CIO or a
	; routine that ends up calling CIO.
	; Therefore, we can use BMI to branch on
	; an error condition.
	BMI ERR1
	JSR CAST
	JSR CLOSE7
	BMI ERROR
	JMP FINE
ERR1 TYA
	PHA
	JSR CAST
	PLA
	TAY

; The error routine uses the error number
; found in the Y register, prints the
; error message with PRMSG, and the error
; number with OUTNUM. The open file is
; closed. If the BREAK key was used to
; stop the operation, we distinguish this
; from an ordinary error, and print
; "BREAK Abort" instead.
ERROR CPY #128
	BEQ STOPPED
	TYA
	PHA
	LDA #125
	JSR CHROUT
	LDA # <ERRMSG
	LDY # >ERRMSG
	JSR PRMSG
	PLA
	TAX
	LDA #0
	JSR OUTNUM
ERXIT JSR IOCLOSE
	JSR HIGHLIGHT
	LDA #1
	STA MSGFLG
	RTS
STOPPED JSR TOPCLR
	LDA # <BRMSG
	LDY # >BRMSG
	JSR PRMSG
	JMP ERXIT

; General file closing routine. IOCB
; contains the channel number times 16.
IOCLOSE LDX IOCB
	LDA #12
	STA ICCOM,X
	JMP CIO

; TOPEN is used to get a filename,
; including the device specification.
; It's used by Save, Load, and Print. It
; forces the CAPS key to uppercase for
; the filename, which is not quite as
; satisfactory as converting the filename
; if lowercase was used. It does return
; the CAPS key to its former value,
; though. TOPEN opens the file and
; returns with the error code in the Y
; register.
TOPEN LDX #$70
	STX IOCB
	STA ACCESS
	; Save current CAPS value.
	LDA SHFLOK
	PHA
	; CAPS On.
	LDA #64
	STA SHFLOK
	JSR INPUT
	; Restore CAPS value.
	PLA
	STA SHFLOK
	LDA INLEN
	BNE OPCONT
OPABORT JSR SYSMSG
	PLA
	PLA
	JMP HIGHLIGHT
OPCONT JSR IOCLOSE
	LDX IOCB
	LDA # <INBUFF
	STA ICBADR,X
	LDA # >INBUFF
	STA ICBADR+1,X
	LDA INLEN
	STA ICBLEN,X
	LDA #0
	STA ICBLEN+1,X
	LDA ACCESS
	STA ICAUX1,X
	LDA #3
	STA ICCOM,X
	JMP CIO

; The Load routine checks the cursor
; position. If the cursor is at the
; top-of-text (CURR=TEXSTART), we call
; the ERASE routine to wipe out memory
; before the load. Otherwise, the load
; starts at the cursor position,
; performing an append, and we change the
; command line to green ($C4, sorry about
; not using a label) to warn the user. We
; open the file for reading by passing a
; 4 to TOPEN, then at LOADLINK use GET
; TEXT (command 7) to get no more than
; the length of the text area. The actual
; length loaded is found in ICBLEN, so we
; add this to TEXSTART and the offset
; between the cursor position and
; TEXSTART to get the position of the
; end-of-text (LASTLINE).

; A funny thing happens, though. Up to
; 255 garbage characters appear following
; an otherwise normal load, after the
; end-of-text. I was never able to figure
; out why (and I puzzled over it for a
; week), so I wrote a stopgap routine to
; just clear out one page past the
; end-of-text. The bug is not fixed per
; se, but it has no effect anymore! I
; still think it must be the fault of the
; operating system (I know...).
TLOAD SEC
	LDA CURR
	SBC TEXSTART
	STA TEX
	STA INDIR
	LDA CURR+1
	SBC TEXSTART+1
	STA TEX+1
	STA INDIR+1
	ORA TEX
	BEQ LOAD2
	LDA #$C4
	STA WINDCOLR
LOAD2 JSR TOPCLR
	LDA # <LOADMSG
	LDY # >LOADMSG
	JSR PRMSG
	LDA #4
	JSR TOPEN
	BPL OKLOD
GOERROR JMP ERROR
OKLOD LDA WINDCOLR
	CMP #$C4
	BEQ NOER
	JSR ERASE
NOER JSR LOADLINK
	CPY #128
	BCC JFINE
	JMP ERROR
JFINE JMP FINE

; Entry point for linked files loading.
LOADLINK LDX IOCB
	LDA CURR
	STA ICBADR,X
	LDA CURR+1
	STA ICBADR+1,X
	SEC
	LDA TEXEND
	SBC CURR
	STA ICBLEN,X
	LDA TEXEND+1
	SBC CURR+1
	STA ICBLEN+1,X
	LDA #7
	STA ICCOM,X
	JSR CIO
	BPL TEXOK
	CPY #136
	BEQ TEXOK
	RTS
TEXOK LDX IOCB
	CLC
	LDA ICBLEN,X
	ADC TEXSTART
	STA LASTLINE
	LDA ICBLEN+1,X
	ADC TEXSTART+1
	STA LASTLINE+1
	CLC
	LDA LASTLINE
	ADC INDIR
	STA LASTLINE
	LDA LASTLINE+1
	ADC INDIR+1
	STA LASTLINE+1
	JSR CASTOIN
	LDA LASTLINE
	STA TEX
	LDA LASTLINE+1
	STA TEX+1
	LDA #0
	TAY
NOGARBAGE STA (TEX),Y
	INY
	BNE NOGARBAGE
	RTS
FINE JSR IOCLOSE
	BPL PROKMSG
	JMP ERROR
PROKMSG LDA #125
	JSR CHROUT
	LDA # <OKMSG
	LDY # >OKMSG
	JSR PRMSG
	JMP ERXIT

; Disable display-list interrupt and
; restore screen colors.
DELITE LDA #$40
	STA $D40E
	LDA SCRCOL
	STA 710
	STA 712
	LDA TEXCOLR
	STA 709
	RTS

; A rather short routine that converts a
; string of ASCII digits into a number in
; hex and the accumulator. It takes
; advantage of decimal mode. In decimal
; mode, the accumulator is adjusted after
; additions and subtractions so that it
; acts like a two-digit decimal counter.
; We shift BCD over a nybble and add in
; the left nybble of the ASCII number
; until we reach the end of the ASCII
; number. We then subtract 1 from BCD and
; increment X (which doesn't conform to
; decimal mode) until BCD is down to 0.
; The X register magically holds the
; converted number. Naturally, decimal
; mode is cleared before this routine
; exits, or it would wreak major havoc.
; ASCHEX is used to convert the
; parameters of printer commands like
; left margin.
ASCHEX LDX #0
	STX BCD
	STX BCD+1
	STX HEX
	STX HEX+1
DIGIT SEC
	LDA (TEX),Y
	SBC #16
	BCC NONUM
	CMP #10
	BCS NONUM
	ASL BCD
	ROL BCD+1
	ASL BCD
	ROL BCD+1
	ASL BCD
	ROL BCD+1
	ASL BCD
	ROL BCD+1
	ORA BCD
	STA BCD
	INY
	BNE DIGIT
	INC TEX+1
	JMP DIGIT
NONUM SED
DECHEX LDA BCD
	ORA BCD+1
	BEQ DONENUM
	SEC
	LDA BCD
	SBC #1
	STA BCD
	LDA BCD+1
	SBC #0
	STA BCD+1
	INC HEX
	BNE NOHEXINC
	INC HEX+1
NOHEXINC JMP DECHEX
DONENUM LDA HEX
	CLD
	RTS

; Insert the buffer. This is the recall
; routine called by CTRL-R. It must not
; allow an insertion that would overfill
; memory. It calls DMOVE to open a space
; in memory, then UMOVE (which is a
; little faster than DMOVE) to copy the
; buffer to the empty space.
INSBUFFER SEC
	LDA TPTR
	SBC TEXBUF
	STA BUFLEN
	LDA TPTR+1
	SBC TEXBUF+1
	STA BUFLEN+1
	ORA BUFLEN
	BNE OKBUFF
	JSR TOPCLR
	LDA # <INSMSG
	LDY # >INSMSG
	JSR PRMSG
	LDA #1
	STA MSGFLG
	RTS
OKBUFF CLC
	LDA CURR
	STA FROML
	ADC BUFLEN
	STA DESTL
	LDA CURR+1
	STA FROMH
	ADC BUFLEN+1
	STA DESTH
	SEC
	LDA LASTLINE
	SBC FROML
	STA LLEN
	LDA LASTLINE+1
	SBC FROMH
	STA HLEN
	CLC
	ADC DESTH
	CMP TEXEND+1
	BCC OKMOV
	JSR TOPCLR
	LDA # <INSERR
	LDY # >INSERR
	JSR PRMSG
	LDA #1
	STA MSGFLG
	RTS
OKMOV JSR DMOVE
	CLC
	LDA BUFLEN
	STA LLEN
	ADC LASTLINE
	STA LASTLINE
	LDA BUFLEN+1
	STA HLEN
	ADC LASTLINE+1
	STA LASTLINE+1
	LDA CURR
	STA DESTL
	LDA CURR+1
	STA DESTH
	LDA TEXBUF
	STA FROML
	LDA TEXBUF+1
	STA FROMH
	JSR UMOVE
	JMP CHECK

; Exchange the character highlighted by
; the cursor with the character to the
; right of it. Not a vital command, but
; it was included due to the brevity of
; the code.
SWITCH LDY #0
	LDA (CURR),Y
	TAX
	INY
	LDA (CURR),Y
	DEY
	STA (CURR),Y
	INY
	TXA
	STA (CURR),Y
	RTS

; Change the case of the character
; highlighted by the cursor.
ALPHA LDY #0
	LDA (CURR),Y
	AND #63
	CMP #33
	BCC NOTALPHA
	CMP #59
	BCS NOTALPHA
	LDA (CURR),Y
	EOR #64
	STA (CURR),Y
NOTALPHA JMP RIGHT

; Convert internal (screen code) format
; to Atari ASCII (ATASCII). Used to
; convert the screen-code format of
; SpeedScript documents to ASCII for the
; sake of printing.
INTOAS PHA
	AND #128
	STA TEMP
	PLA
	AND #127
	CMP #96
	BCS XINT
INCONT CMP #64
	BCC INT1
	SBC #64
	JMP XINT
INT1 ADC #32
XINT ORA TEMP
	RTS

; The start of the printer routines. This
; part could logically be called a
; separate program, but many variables
; are common to the above code.

; DEFTAB: Table of default settings for
; left margin, right margin, page length,
; top margin, bottom margin, etc. See the
; table starting at LMARGIN at the end of
; this source code.
DEFTAB .BYTE 5,75,66,5,58,1,1,1,0,1,0,80

; Table of default printer codes.
PRCODES .BYTE 27,14,15,18

; Another advantage of modular coding is
; that you can change the behavior of a
; lot of code by just changing one small
; common routine. This is a substitute
; for the normal CHROUT routine. It
; checks to see if the current page
; number equals the page number specified
; by the user to start printing. It also
; checks for the BREAK to abort the
; printing and permits printing to be
; paused with CTRL-1.
PCHROUT STA PCR
	TXA
	PHA
	TYA
	PHA
	SEC
	LDA PAGENUM
	SBC STARTNUM
	LDA PAGENUM+1
	SBC STARTNUM+1
	BCC SKIPOUT
	LDA #1
	STA 766
	LDX #$70
	LDA #0
	STA ICBLEN,X
	STA ICBLEN+1,X
	LDA #11
	STA ICCOM,X
	LDA PCR
	JSR CIO
	PHP
	LDA #0
	STA 766
	PLP
	BPL SHIFTFREEZE
ERRLINK JSR ERROR
	LDX #$FA
	TXS
	JMP MAIN
SHIFTFREEZE LDA $02FF ;CTRL-1
	BNE SHIFTFREEZE
SKIPOUT PLA
	TAY
	PLA
	TAX
	LDA PCR
	RTS

; Displays "Printing..."
PRIN JSR TOPCLR
	LDA # <PRINMSG
	LDY # >PRINMSG
	JMP PRMSG
PBORT JMP PEXIT

; Called by CTRL-P. We get the filename
; to print to (usually P:, although you
; can use E: to print to the screen) with
; ICAUX1 set to 8 for output. We exit on
; any error. The DELITE routine turns off
; the display-list interrupt, which might
; otherwise interfere with output timing.
PRINT JSR TOPCLR
	LDA # <FNMSG
	LDY # >FNMSG
	JSR PRMSG
	JSR DELITE
	LDA #8
	JSR TOPEN
	BPL PROK
	JMP PEXIT

; Reset several flags (footer length,
; header length, true ASCII, underline
; mode, and linefeed mode). Notice how
; DELITE is called again. This isn't a
; mistake. The first time we called
; DELITE, we then may have opened a file
; to the Editor device. This reset the
; screen to the default colors, so the
; second DELITE retains the user's true
; color choice.
PROK JSR DELITE
	JSR PRIN
	LDX #0
	STX FTLEN
	STX HDLEN
	STX NEEDASC
	STX UNDERLINE
	STX LINEFEED
	; Copy definition tables and default
	; printer codes.
COPYDEF LDA DEFTAB,X
	STA LMARGIN,X
	INX
	CPX #12
	BNE COPYDEF
	LDA #$FF
	STA LINE
	STA NOMARG
	LDX #4
COPYDEFS LDA PRCODES-1,X
	STA CODEBUFFER+16,X
	DEX
	BNE COPYDEFS
	; Reentry point for printing after linked
	; files.
RETEX LDA TEXSTART
	STA TEX
	LDA TEXSTART+1
	STA TEX+1

; Main printing loop. We print the left
; margin, grab a line of text, scan
; backward until we find a space or a
; carriage return, then break the line
; there. If printer codes are
; encountered, they're passed on to the
; SPECIAL routine. Otherwise, we end up
; calling BUFPRT to print the line and
; process some other control codes.
PLOOP LDY #0
	STY POS
	CPY NOMARG
	BEQ PLOOP1
	LDA LMARGIN
	STA POS
PLOOP1 LDA (TEX),Y
	BPL NOTSP
	JMP SPECIAL
NOTSP CMP #RETCHAR
	BEQ FOUNDSPACE
NOTRET STA PRBUFF,Y
	INY
	INC POS
	LDA POS
	CMP RMARGIN
	BCC PLOOP1
	STY FINPOS
FINDSPACE LDA (TEX),Y
	CMP #SPACE
	BEQ FOUNDSPACE
	DEC POS
	DEY
	BNE FINDSPACE
	LDY FINPOS
FSPACE INY
	LDA (TEX),Y
	CMP #SPACE
	BEQ FOUNDSPACE
	DEY
FOUNDSPACE STY FINPOS
OVERSTOR TYA
	SEC
	ADC TEX
	STA TEX
	LDA TEX+1
	ADC #0
	STA TEX+1
	LDY #0
	; If this is the first page, we need to print
	; the header, if any, with JSR TOP.
DOBUFF LDA LINE
	CMP #$FF
	BNE DOBUF2
	JSR TOP
DOBUF2 LDA NOMARG
	BEQ OVERMARG
	JSR LMARG
OVERMARG SEC
	ROL NOMARG
	LDA FINPOS
	STA ENDPOS
	LDA # <PRBUFF
	STA INDIR
	LDA # >PRBUFF
	STA INDIR+1
	JSR BUFPRT
	; A line has been printed. We check to
	; see if we've hit the bottom margin and,
	; if so, go to PAGE, which goes to the
	; end of the page, prints the footer (if
	; any), and feeds to the next page.
ZBUFF JSR CRLF
	LDA LINE
	CMP BOTMARG
	BCC NOTPAGE
	JSR PAGE
	; Have we reached the end-of-text?
NOTPAGE SEC
	LDA TEX
	SBC LASTLINE
	STA TEMP
	LDA TEX+1
	SBC LASTLINE+1
	ORA TEMP
	BEQ DORPT
	BCC DORPT
	; If so, we check for a footer. If there
	; is one, we set HDLEN and TOPMARG to 0
	; (so that the printhead will end up at
	; the right place on the last page) and
	; call PAGE, which prints the footer. If
	; there is no footer, we leave the
	; printhead on the same page so that
	; paper isn't wasted.
	LDA FTLEN
	BEQ PXIT
	LDA #0
	STA HDLEN
	STA TOPMARG
	JSR PAGE
	; Exit routines. If screen output was
	; selected, we wait for a keystroke
	; before going back to editing mode.
PXIT LDA INBUFF
	CMP #'E
	BNE PEXIT
	LDA #155
	JSR CHROUT
	LDA # <DIRMSG
	LDY # >DIRMSG
	JSR PRMSG
	JSR GETAKEY
PEXIT JSR CLOSE7
	LDX #$FA
	TXS
	JSR HIGHLIGHT
	LDA #125
	JSR CHROUT
	JSR SYSMSG
	JMP MAIN
DORPT JMP PLOOP

; Paging routines. We skip (PAGELENGTH -
; LINE) - two blank lines to get to the
; bottom of the page, print a footer (if
; there is one) or a blank line (if not),
; then page to the beginning of the next
; page, skipping over the paper
; perforation. If the wait mode is
; enabled, we wait for the user to insert
; a new sheet of paper.
PAGE SEC
	LDA PAGELENGTH
	SBC LINE
	TAY
	DEY
	DEY
	BEQ NOSK
	BMI NOSK
NEXPAGE JSR CR
	DEY
	BNE NEXPAGE
NOSK LDA FTLEN
	BEQ SKIPFT
	STA ENDPOS
	LDA # <FTBUFF
	STA INDIR
	LDA # >FTBUFF
	STA INDIR+1
	JSR LMARG
	JSR BUFPRT
SKIPFT JSR CR
	JSR CR
	JSR CR
	; Increment the page number.
	INC PAGENUM
	BNE NOIPN
	INC PAGENUM+1
	; The page wait mode is inappropriate
	; when printing to the screen or to disk,
	; or when skipping over pages with the ?
	; format command.
NOIPN LDA CONTINUOUS
	BNE TOP
	SEC
	LDA PAGENUM
	SBC STARTNUM
	LDA PAGENUM+1
	SBC STARTNUM+1
	BCC TOP
	JSR TOPCLR
	LDA # <WAITMSG
	LDY # >WAITMSG
	JSR PRMSG
	JSR GETAKEY
	JSR PRIN
	; Print the header; skip to the top
	; margin.
TOP LDA HDLEN
	BEQ NOHEADER
	STA ENDPOS
	LDA # <HDBUFF
	STA INDIR
	LDA # >HDBUFF
	STA INDIR+1
	JSR LMARG
	JSR BUFPRT
NOHEADER LDY TOPMARG
	STY LINE
	DEY
	BEQ SKIPTOP
	BMI SKIPTOP
TOPLP JSR CR
	DEY
	BNE TOPLP
SKIPTOP RTS

; Left margin routine. This routine is not
; called if NOMARG is selected (margin
; release).
LMARG LDA #32
	LDY LMARGIN
	STY POS
	BEQ LMEXIT
LMLOOP JSR PCHROUT
	DEY
	BNE LMLOOP
LMEXIT RTS

; CRLF is called at the end of most
; printed lines. It increments the LINE
; count and takes into account the
; current line spacing mode set by the a
; format command.
CRLF LDY SPACING
	CLC
	TYA
	ADC LINE
	STA LINE
CRLOOP JSR CR
	DEY
	BNE CRLOOP
	RTS

; CR just prints a single carriage return
; and linefeed (if specified).
CR  LDA #155
	JSR PCHROUT
	LDA LINEFEED
	BEQ NOLF
	JSR PCHROUT
NOLF RTS

; Handle special printer codes like left
; margin. This looks up the printer code
; using a routine similar to CONTROL.
SPECIAL STA SAVCHAR
	AND #127
	JSR INTOAS
	LDX SPTAB
SRCHSP CMP SPTAB,X
	BEQ FSP
	DEX
	BNE SRCHSP
	DEC POS
	JMP DEFINE
FSP DEX
	TXA
	ASL A
	TAX
	STY YSAVE
	LDA # >(SPCONT-1)
	PHA
	LDA # <(SPCONT-1)
	PHA
	LDA SPVECT+1,X
	PHA
	LDA SPVECT,X
	PHA
	RTS

; After the format code is processed, we
; must skip over the format command
; and its parameter so that it's not
; printed.
SPCONT SEC
	LDA YSAVE
	ADC TEX
	STA TEX
	LDA TEX+1
	ADC #0
	STA TEX+1
	JMP PLOOP

; If the format command ends with a
; return-mark, we must skip over the
; return-mark as well.
SPCEXIT LDA (TEX),Y
	CMP #RETCHAR
	BEQ NOAD
	DEY
NOAD STY YSAVE
	RTS

; Special format code table. It starts with
; the number of format commands, then
; the characters for each format
; command.
SPTAB .BYTE 17
	.BYTE "wlrtbsnhf@p?xmigj"

; The address-1 of each format routine.
SPVECT .WORD PW-1,LM-1,RM-1,TP-1
	.WORD BT-1,SP-1,NX-1,HD-1,FT-1
	.WORD PN-1,PL-1,SPAGE-1,ACROSS-1
	.WORD MRELEASE-1,COMMENT-1,LINK-1
	.WORD LFSET-1

; m Margin release. INY is used to skip
; over the format character.
MRELEASE INY
	LDA #0
	STA NOMARG
	JMP SPCEXIT

; x Columns across, used by centering.
ACROSS INY
	JSR ASCHEX
	STA PAGEWIDTH
	JMP SPCEXIT

; ? Start printing at specified page.
SPAGE INY
	JSR ASCHEX
	STA STARTNUM
	LDA HEX+1
	STA STARTNUM+1
	JMP SPCEXIT

; @ Set starting default page number.
PN  INY
	JSR ASCHEX
	STA PAGENUM
	LDA HEX+1
	STA PAGENUM+1
	JMP SPCEXIT

; p Page length.
PL  INY
	JSR ASCHEX
	STA PAGELENGTH
	JMP SPCEXIT

; w Set page wait mode.
PW  LDA #0
	STA CONTINUOUS
	INY
	JMP SPCEXIT

; j Set linefeed mode.
LFSET LDA #10
	STA LINEFEED
	INY
	JMP SPCEXIT

; l Left margin.
LM  INY
	JSR ASCHEX
	STA LMARGIN
	JMP SPCEXIT

; r Right margin.
RM  INY
	JSR ASCHEX
	STA RMARGIN
	JMP SPCEXIT

; t Top margin.
TP  INY
	JSR ASCHEX
	STA TOPMARG
	JMP SPCEXIT

; b Bottom margin.
BT  INY
	JSR ASCHEX
	STA BOTMARG
	JMP SPCEXIT

; s Set line spacing.
SP  INY
	JSR ASCHEX
	STA SPACING
	JMP SPCEXIT

; n Jump to next page.
NX  LDY YSAVE
	INY
	TYA
	PHA
	JSR PAGE
	PLA
	TAY
	STY YSAVE
	RTS

; h Define header. Copy header into
; header buffer.
HD  JSR PASTRET
	DEY
	STY HDLEN
	LDY #1
HDCOPY LDA (TEX),Y
	STA HDBUFF-1,Y
	INY
	CPY HDLEN
	BCC HDCOPY
	BEQ HDCOPY
	INY
	JMP SPCEXIT

; Skip just past the return-mark.
PASTRET INY
	LDA (TEX),Y
	CMP #RETCHAR
	BNE PASTRET
	RTS

; f Define footer.
FT  JSR PASTRET
	DEY
	STY FTLEN
	LDY #1
FTCOPY LDA (TEX),Y
	STA FTBUFF-1,Y
	INY
	CPY FTLEN
	BCC FTCOPY
	BEQ FTCOPY
	JMP SPCEXIT

; i Ignore a line of information.
COMMENT JSR PASTRET
	JMP SPCEXIT

; Define programmable printkeys. We
; check for =. If not found, this is not an
; assignment, so we just skip past the
; code. Otherwise, we use the screen
; code value as the index into the
; CODEBUFFER and put the value there,
; ready to be called during printing by
; BUFPRT.
DEFINE INY
	LDA (TEX),Y
	CMP #'=-32
	BEQ DODEFINE
	DEY
	LDA SAVCHAR
	JMP NOTRET
DODEFINE INY
	JSR ASCHEX
	PHA
	LDA SAVCHAR
	AND #127
	TAX
	PLA
	STA CODEBUFFER,X
	JSR SPCEXIT
	JMP SPCONT

; g Link to next file. We get the
; filename from text and put it into the
; input buffer, just as if the filename
; were typed in with INPUT. We then jump
; into the TOPEN routine to open the
; file, and into the Load routine to load
; the file. After the load, we check for
; a load error, then jump to RETEX to
; continue printing.
LINK LDY #1
	LDX #0
FNCOPY LDA (TEX),Y
	CMP #RETCHAR
	BEQ FNEND
	JSR INTOAS
	STA INBUFF,X
	INY
	INX
	CPX #14
	BNE FNCOPY
FNEND STX INLEN
	LDA #0
	STA INBUFF,X
	LDX #$60
	STX IOCB
	LDA #4
	STA ACCESS
	JSR OPCONT
	BPL LNOERR
	JMP ERRLINK
LNOERR LDA #0
	STA INDIR
	STA INDIR+1
	JSR ERASE
	JSR LOADLINK
	BPL LCONT
	JMP ERRLINK
LCONT PLA
	PLA
	LDX #$70
	STA IOCB
	JMP RETEX

; Global search and replace. This just
; links together the search-specify
; routine, the replace-specify routine,
; then repeatedly calls Hunt and Replace,
; until Hunt returns "Not Found." (FPOS+1
; is $FF after a search failure.)
SANDR JSR RESET
	LDA HUNTLEN
	BEQ NOSR
	JSR ASKREP
SNR JSR CONTSRCH
	LDA FPOS+1
	CMP #$FF
	BEQ NOSR
	JSR REPL
	JSR REFRESH
	JMP SNR
NOSR JMP SYSMSG

; If OPTION is held down with CTRL-F, we
; ask for and store the search phrase. If
; OPTION is not down, we perform the
; actual search. The line in the INBUFF
; is compared with characters in text. If
; at any point the search fails, we
; continue the comparison with the first
; character of INBUFF. The search is a
; failure if we reach the end-of-text. If
; the entire length of INBUFF matches,
; the search succeeds, so we change the
; CURRent cursor position to the found
; position, save the found position for
; the sake of the replace routine, then
; call CHECK to scroll to the found
; position.
HUNT LDA #8
	STA 53279
	LDA 53279
	CMP #3
	BNE CONTSRCH
RESET JSR TOPCLR
	LDA # <SRCHMSG
	LDY # >SRCHMSG
	JSR PRMSG
	JSR INPUT
	STA HUNTLEN
	BNE OKSRCH
	JMP SYSMSG
OKSRCH LDY #0
TOBUFF LDA INBUFF,Y
	STA HUNTBUFF,Y
	INY
	CPY INLEN
	BNE TOBUFF
	JMP SYSMSG
CONTSRCH LDA CURR
	STA TEX
	LDA CURR+1
	STA TEX+1
	LDA #$FF
	STA FPOS+1
	LDY #1
SRCH0 LDX #0
	LDA HUNTLEN
	BEQ NOTFOUND
SRCH1 LDA HUNTBUFF,X
	JSR ASTOIN
	CMP (TEX),Y
	BEQ CY
	CPX #0
	BNE SRCH0
	DEX
CY  INY
	BNE NOVFL
	INC TEX+1
	LDA TEX+1
	CMP LASTLINE+1
	BEQ NOVFL
	BCS NOTFOUND
NOVFL INX
	CPX HUNTLEN
	BNE SRCH1
	CLC
	TYA
	ADC TEX
	STA TEMP
	LDA TEX+1
	ADC #0
	STA TEMP+1
	LDA LASTLINE
	CMP TEMP
	LDA LASTLINE+1
	SBC TEMP+1
	BCC NOTFOUND
	SEC
	LDA TEMP
	SBC HUNTLEN
	STA CURR
	STA FPOS
	LDA TEMP+1
	SBC #0
	STA CURR+1
	STA FPOS+1
	JSR CHECK
	RTS
NOTFOUND JSR TOPCLR
	LDA # <NFMSG
	LDY # >NFMSG
	JSR PRMSG
	LDA #1
	STA MSGFLG
	RTS

; The change (replace) routine checks to
; see if OPTION is held down with CTRL-C.
; If it is, we ask for a replace phrase,
; and exit. If not, we check to see if
; the cursor is at the position
; previously located by the search
; routine. If it is, we delete the found
; phrase, then insert the replace phrase.
; The cursor is moved past the replace
; phrase for the sake of the next search.
; This also prevents endless recursion,
; as in replacing in with winner.
REPSTART LDA #8
	STA 53279
	LDA 53279
	CMP #3
	BNE REPL
ASKREP JSR TOPCLR
	LDA # <REPMSG
	LDY # >REPMSG
	JSR PRMSG
	JSR INPUT
	STA REPLEN
	BEQ NOREP
	LDY #0
REPMOV LDA INBUFF,Y
	STA REPBUFF,Y
	INY
	CPY INLEN
	BNE REPMOV
NOREP JMP SYSMSG
REPL SEC
	LDA CURR
	STA DESTL
	SBC FPOS
	STA TEMP
	LDA CURR+1
	STA DESTH
	SBC FPOS+1
	ORA TEMP
	BNE NOREPL
	LDA #$FF
	STA FPOS+1
	CLC
	LDA HUNTLEN
	ADC CURR
	STA FROML
	LDA #0
	ADC CURR+1
	STA FROMH
	SEC
	LDA LASTLINE
	SBC DESTL
	STA LLEN
	LDA LASTLINE+1
	SBC DESTH
	STA HLEN
	JSR UMOVE
	SEC
	LDA LASTLINE
	SBC HUNTLEN
	STA LASTLINE
	LDA LASTLINE+1
	SBC #0
	STA LASTLINE+1
	LDA REPLEN
	BEQ NOREPL
	STA INSLEN
	LDA #0
	STA INSLEN+1
	JSR INSBLOCK
	LDY #0
REPLOOP LDA REPBUFF,Y
	JSR ASTOIN
	STA (CURR),Y
	INY
	CPY REPLEN
	BNE REPLOOP
	CLC
	LDA CURR
	ADC REPLEN
	STA CURR
	LDA CURR+1
	ADC #0
	STA CURR+1
NOREPL JMP CHECK

; Suddenly, we're back to a PRINT
; subroutine. This examines the buffer as
; it's being printed, checking for
; printkeys and Stage 2 commands like
; centering.
BUFPRT LDY #0
BUFLP CPY ENDPOS
	BEQ ENDBUFF
	LDA (INDIR),Y
	BMI SPEC2
	JSR INTOAS
	JSR PCHROUT
	; In underline mode, after we print the
	; character, we backspace the printhead
	; and print an underline character.
	LDA UNDERLINE
	BEQ NOBRK
	LDA #8
	JSR PCHROUT
	LDA #95
	JSR PCHROUT
NOBRK INY
	JMP BUFLP
ENDBUFF RTS

; Stage 2 format commands.
SPEC2 STY YSAVE
	AND #127
	STA SAVCHAR
	JSR INTOAS
OTHER CMP #'c
	BNE NOTCENTER
	; c Centering looks at the length of the
	; line, then sends out extra spaces (the
	; left margin has already been printed) to
	; move the printhead to the right place.
	SEC
	LDA PAGEWIDTH
	SBC ENDPOS
	LSR A
	SEC
	SBC LMARGIN
	TAY
	LDA #32
CLOOP JSR PCHROUT
	DEY
	BNE CLOOP
	LDY YSAVE
	JMP NOBRK
NOTCENTER CMP #'e
	BNE NOTEDGE
	; e Edge right. This subtracts the length
	; of the line from the right margin
	; position and moves the printhead to
	; this position.
EDGE SEC
	LDA RMARGIN
	SBC ENDPOS
	SEC
	SBC LMARGIN
	TAY
	LDA #32
	JMP CLOOP
NOTEDGE CMP #'u
	BNE NOTOG
	; u Toggle underline mode.
	LDA UNDERLINE
	EOR #1
	STA UNDERLINE
NOTOG CMP #'#
	BNE DOCODES
	; # Substitute the current page number
	; for the # symbol.
DOPGN STY YSAVE
	LDX PAGENUM
	LDA PAGENUM+1
	JSR PROUTNUM
	LDY YSAVE
	JMP NOBRK

; Do special format codes. This just uses
; the screen-code value of the character
; as an index into the CODEBUFFER, then
; sends out the code. SpeedScript makes
; no judgment on the code being sent out.
DOCODES LDX SAVCHAR
	LDA CODEBUFFER,X
	JSR PCHROUT
	JMP NOBRK

; Display free memory using OUTNUM.
FREEMEM JSR TOPCLR
	SEC
	LDA TEXEND
	SBC LASTLINE
	TAX
	LDA TEXEND+1
	SBC LASTLINE+1
	JSR OUTNUM
	LDA #1
	STA MSGFLG
	RTS
